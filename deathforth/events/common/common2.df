
cyoa{ road{ 

: camp-pic
	50 %chance if
		image-camp-1
	else
		image-camp-2
	then	
;

// Rares

cyoa-choice: common2-rare-tp-share
	someicon
	solo if
		($ cname .. " se da cuenta de que este rollo de papel higienico es un tesoro que no debe ser regalado." $) text! morale4+ goodfx 1 WEAPON_CARDBOARD trunk.weapon+
		zombop++
	then;
	"El equipo decide intentar compartir el rollo de papel higienico." text!
	loyalty-average 2 < if
		"No les toma mucho tiempo entrar en una gran pelea! La situacion se desmorona en una batalla total. El papel higienico acaba siendo arrancado por error!" +crtext morale3-all badfx
	else
		loyalty-average 4 < if
			"Hay algunas peleas inevitables al compartir el TP, pero el grupo logra superar sus diferencias." +crtext morale2+all goodfx
		else
			"El grupo se une en torno al papel higienico, protegiendose mutuamente. Aun despues de que el papel se haya ido, la tuberia de carton se convierte en un simbolo para la unidad del grupo." +crtext morale4+all goodfx
		then
	then
	' .loyaltyREV foreach-team-char 1 WEAPON_CARDBOARD trunk.weapon+
	zombop++
;

cyoa-choice: common2-rare-tp-trade
	someicon
	($ Thegroup .. " ' decide vender el rollo de papel higienico lo antes posible. No vale llevar algo TAN valioso alrededor!" $) text!
		50 %chance if
			"Un asentamiento compra el rollo de papel higienico a cambio de 20 alimentos! No malo!" +crtext 20 trunk-food+
		else
			50 %chance if
				"Un mercader astuto compra el papel higienico a cambio de 12 alimentos. No se quejar!" +crtext 12 trunk-food+
			else
				($ "Un " .. "MUY" rbold .. " 'interesado participate compra el papel higienico por " .. "30 alimentos!" fbold $) +crtext 30 trunk-food+
			then
		then goodfx morale++all
;

cyoa: common2-rare-tp
	"Mas Valioso Que Oro" cyoa-title!
	($ Thegroup .. " 'encontra un " .. "ROLLA SUPREMA DE PAPEL HIGIENICO" fbold .. " 'mientras buscaba. El papel higienico fue la primera cosa en agotarse despues del apocalipsis. Aun quedan toneladas de comida enlatada y municiones en America. El papel higienico de buena calidad se acabo rapidamente. Elige sabiamente!" $) text!
		solo if
			choice( "Usa este precioso regalo" )choice: common2-rare-tp-share
		else
			choice( "Comparte este regalo precioso" )choice: common2-rare-tp-share
		then
		choice( "Cambialo ya" )choice: common2-rare-tp-trade
;

cyoa-choice: common2-rare-zombmaster-run
	image-creepy-girl
	($ "El " .. "MAESTRO DE ZOMBIES" rbold .. "  'establece un pequeno curso de obstaculos y ensena' " .. thegroup .. " 'como correr de una manera mas eficiente, menos embarazosa!" $) text! goodfx
	' .dexterity++ foreach-team-char
;

: common2-rare-zomb-workout args( @ )
	<- @ .strengthREV <- @ .fitnessREV <- @ .strength++ <- @ .fitness++
;

cyoa-choice: common2-rare-zombmaster-fight
	image-creepy-girl
	($ "El " .. "MAESTRO DE ZOMBIES" rbold .. " ' ilumina " .. thegroup .. " ' en el armamento de combate cuerpo a cuerpo con algunas recomendaciones y alguna " .. "ENTRENAMIENTO MUY Peligroso." rbold $) text! goodfx bitefx
	' common2-rare-zomb-workout foreach-team-char sting--all
;

cyoa-choice: common2-rare-zombmaster-shoot
	image-creepy-girl
	($ "El " .. "MAESTRO DE ZOMBIES" rbold .. " ' otorga " .. thegroup .. " ' una gran leccion sobre el tiro correcto! Ella asigna un trabajo de casa para hacer caza real de zombis, y provee una cantidad grande de municiones y un fusil!" $) text! goodfx ' .shootingREV foreach-team-char ' .shooting++ foreach-team-char
	60 trunk-bullet+ 40 trunk-rifle-shell+ 30 trunk-shotgun-shell+ 1 WEAPON_RIFLE trunk.weapon+
;

cyoa-choice: common2-rare-zombmaster-tough
	image-creepy-girl
	($ "El " .. "MAESTRO DE ZOMBIES" rbold .. " grunce." .. thegroup .. " ' endurecer " .. ghis .. " ' piel contra mordeduras de zombis! Este proceso involucra su golpearlo. " .. ghim .. " 'me" $) text! goodfx bitefx
	' .vitality++ foreach-team-char sting--all
;

cyoa: common2-rare-zombmaster
	image-creepy-girl "El Maestro de Zombies" cyoa-title!
	($ Thegroup .. " ' se topa con una extrana mujer usando gafas, un escarfeto y un capuz. Ella dice ser la " .. "MAESTRO DE ZOMBIES" rbold .. " 'y que ensenara uno de sus secretos sobre el tiro a los zombis como un servicio a la humanidad." $) text! goodfx
		choice( "Leccion de tiro a zombies" )choice: common2-rare-zombmaster-shoot
		choice( "Huye constantemente" )choice: common2-rare-zombmaster-run
		choice( "Como luchar con arma cuerpo a cuerpo" )choice: common2-rare-zombmaster-fight
		choice( "Endurece contra mordeduras" )choice: common2-rare-zombmaster-tough
;

// END rares

// Man trapped under cardboard thievery

0 value common2-cboardtype

: cboardprize
	common2-cboardtype case
		1 of "Porra" endof
		2 of "ESCOPETA" endof
		3 of "FUSIL" endof
		4 of "REVOLVER DOLT45" endof
		5 of "HACHA DE FUEGO" endof
		6 of "REVOLVER SNUBNOSE" endof
	endcase
;

: cboardget
	common2-cboardtype case
		1 of 1 WEAPON_NIGHTSTICK endof
		2 of 8 trunk-shotgun-shell+ 1 WEAPON_SHOTGUN endof
		3 of 12 trunk-rifle-shell+ 1 WEAPON_RIFLE endof
		4 of 6 trunk-bullet+ 1 WEAPON_DOLT endof
		5 of 1 WEAPON_FIRE endof
		6 of 6 trunk-bullet+ 1 WEAPON_SNUBNOSE endof
	endcase
;

cyoa-choice: common2-cboard-steal
	($ cname .. " 'roba al hombre indefenso " .. cboardprize rbold .. "." $) text! badfx
	cloyalty- if
		($ cHe .. " 'se siente bastante satisfecho con esto." $) +crtext morale++
	else
		cloyalty+ if
			($ cHe .. " 'se siente increiblemente culpable por esto!" $) +crtext morale3-
		else
			($ cHe .. " 'se siente culpable por esto." $) +crtext morale--
		then
	then loyaltyREV
	($ "Obtuvo un " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+
;

: cboard-help-check args( @ )
	<- @ .cloyalty- if <- @ .loyaltyREV <- @ .morale-- then; <- @ .morale++
;

cyoa-choice: common2-cboard-help
	($ Thegroup .. " 1" $) text! ' cboard-help-check foreach-team-char goodfx
;

cyoa-choice: common2-cboard-bandit
	($ cname .. " 'roba al hombre indefenso " .. cboardprize rbold .. " y luego toma el resto de sus suministros!" $) text! badfx
	rob-guilt	
	($ "Obtuvo un " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+ 
	10 20 rnd trunk-bullet+ 5 15 rnd trunk-rifle-shell+ 3 12 rnd trunk-shotgun-shell+
	2 4 rnd trunk-medical+ 1 6 rnd trunk-food+
	aholeREV
;

cyoa-bridge: common2-cboard-steal4
	someicon
	"Quien deberia robar su arma?" cyoa-text!
	' common2-cboard-steal cyoa-team-choice
;

cyoa: common2-cboard
	"Ayudar a alguien en necesidad!" cyoa-title!
	1 6 rnd to common2-cboardtype
	($ "Mientras buscas, " .. thegroup .. " 'corre hacia un hombre cubierto de cajas de carton. El dice que ha estado alli durante dias! El tiene un " .. cboardprize rbold .. " 'que esta en el suelo fuera de su alcance." $) text!
	choice( "Ayudenlo a levantarse" )choice: common2-cboard-help
	most-ahole if
		charchoice( ": Robarle todo" )choice: common2-cboard-bandit
		team-count 2 > if
			choice( "Elige a un ladron" )choice: common2-cboard-steal4
		then;
	then	
		
	solo if
		soloer choosechar choice( "Robarle su arma" )choice: common2-cboard-steal
	else
		"Deberia robarsele su arma?" +crtext
		team-count 3 > if
			choice( "Elige a un ladron" )choice: common2-cboard-steal4
		then 
		' common2-cboard-steal 3 #cyoa-team-choice
	then
;

// Quick template for the usual "elija a alguien mas" choice
(*

cyoa-bridge: 4
	someicon
	"Quien debe ?" cyoa-text!
	'  cyoa-team-choice
;

		team-count 3 > if
			'  2 #cyoa-team-choice
			choice( "Elige a alguien mas" )choice: 4
		else

		then

*)

// Farm help

: common2-farm-reward
	8 trunk-food+
	most-charming if
		choosechar ($ cname .. " 'convence a los granjeros para dar " .. chim .. " ' un palo con forma de garrocha!" $) +crtext
		1 WEAPON_PITCHFORK trunk.weapon+
	then
;

cyoa-choice: common2-farm-mechanic
	($ cname .. " ofertas " .. chis .. "  servicios en arreglar cosas en la granja. " $) text!
	cmechanic+ if	
		($ cHe .. " ' es tan habil que el gran volumen de trabajo no es un problema grande." $) +text
	else
		($ "Hay tantas cosas rotas que " .. che .. " ' se agota." $) +text cchar tired
	then
	mechanicREV mechanic++
	cchar .mechanical 3 < if
		($ "Los campesinos rehusan premiar a alguien despues de que descubren que todo sigue roto." $) +crtext badfx
	else
		($ "Los campesinos estan agradecidos por el trabajo!" $) +crtext goodfx common2-farm-reward
	then
;

cyoa-choice: common2-farm-bandit
	($ cname .. " ' roba a los tranquilos campesinos. Ellos fueron completamente sorprendidos contra una amenaza no zombie." $) text! badfx
	14 trunk-food+ rob-guilt2 aholeREV
;

cyoa-choice: common2-farm-haul
	someicon
	($ Thegroup .. " 'transporta toneladas de cosas a traves de un camino. Hay siempre cosas que transportar en una granja. Es trabajo agotador, y " .. gthey .. " ' obtiene" .. g1s .. " ' se encuentra muy cansado!" $) text! goodfx 
	' .strengthREV foreach-team-char ' .strength++ foreach-team-char common2-farm-reward
	// added STR check for scythe
	strongest-pass if strongest choosechar
		($ "Los campesinos notaron " .. cname .. "su fuerza extrana y le dieron " .. chim .. " 'una faja para cortar el trigo como regalo!" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa-choice: common2-farm-till
	someicon
	($ Thegroup .. " 1 " .. gthey .. " ' obtiene" .. g1s .. " ' se encuentra muy cansado!" $) text! goodfx 
	' .fitnessREV foreach-team-char ' .fitness++ foreach-team-char common2-farm-reward
	// added FIT check for scythe
	fittest-pass if fittest choosechar
		($ "Los campesinos notaron " .. cname .. "'s level of fitness and gave " .. chim .. " 'una faja para cortar el trigo como regalo!" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa: common2-farm
	"Un viaje a la granja" cyoa-title!
	($ Thegroup .. " 8 food" $) text!
		choice( "Labranza y siembra" )choice: common2-farm-till
		choice( "Arrastrar y arrastrar" )choice: common2-farm-haul
		most-ahole if
			charchoice( "robar la granja" )choice: common2-farm-bandit
		then
		mechanicest	charchoice( ": Trabajo de reparacion" )choice: common2-farm-mechanic
		choice( "No hay tiempo para ayudar" )choice
;

// Annoying diplomat

: common2-detoured
	vehicle.which@ 0 > if choice( "Tomar un desvio" )choice: car-detour else
		// ($ Thegroup .. " ' toma una larga desvio a pie." $) +crtext 
		1 calendar-day+ +cr fullcamp
	then
;

cyoa-bridge: common2-meeting-talk
	($ cname .. " ' habla con el representante molesto." $) text!
	cpet? if
		($ cHe .. " ' no esta supuesto ser capaz de hablar, asi que " .. che .. " ' se dedica principalmente a lamer " .. chim .. "el mismo. El dingus se ofende mucho por ser obligado a hablar con un " .. cdogcat .. ", pero los guardias estan divertidos. Dejan " .. thegroup .. " ' pasar!" $) +crtext goodfx morale++all
	then;
	cchar grating? if
		($ cHe .. " ' se entienden maravillosamente! Pasan la hora principalmente intercambiando chistes con cada otro. " .. "CALMA" pbold .. " ' es dejado pasar. " .. Thegroup .. " ' se le deja pasar." $) +crtext witsattREV goodfx cool-it++ cool-it++
	then;
	cchar oblivious? if
		($ cHe .. "  the dingus is hired. " .. thegroup .. "  is let past." $) +crtext witsattREV goodfx
	then;
	cattitude+ if
		($ cHe .. "  logra ser muy educado. Despues de suficiente cuestionamiento sin sentido, " .. thegroup .. "  se deja pasar a traveÌs de la barrera." $) +crtext attitudeREV goodfx
	then;
	cchar angrystrong? if
		($ cHe .. " ' pierde " .. chis .. "  go through. " .. thegroup .. " de todos modos" $) +crtext attitudeREV strengthREV goodfx morale++all
	then;
	ctemper- if
		($ cHe .. " ' pierde " .. chis .. "  se enoja de inmediato y comienza a gritar. Esto realmente asusta a los guardias, y comienzan a disparar. " .. Thegroup .. " ' debe huir!" $) +crtext temperREV bitefx sting--all
		common2-detoured	
	then;
	ctemper+ temperroll or if
		($ cHe .. " ' logra dominarlo " .. chis .. " ' temperamento! " .. Thegroup .. " ' es dejado pasar a traves de la barricada despues de algunas molestias adicionales." $) +crtext goodfx
	else
		($ cHe .. " ' intenta dominarlo " .. chis .. " ' temperamento, pero la persona es simplemente demasiado molesta. Ella corre y dice a las guardias para que no abran la puerta. " .. Thegroup .. " ' tiene que encontrar una forma de desviarse!" $) +crtext badfx
		common2-detoured
	then temperREV
;

cyoa-bridge: common2-meeting-talk4
	someicon
	"Quien debe tratar con la persona molesta?" cyoa-text!
	' common2-meeting-talk cyoa-team-choice
;

cyoa: common2-meeting
	"Diplomacia de calma" cyoa-title!
	($ Thegroup .. " ' esta bloqueado por otro grupo de supervivientes que han establecido una barricada para protegerse. Estan desconfiados y bien armados. La persona que envian a hablar es " .. "EXTREMADAMENTE ALILO" rbold .. "." $) text!
		solo if
			($ " ' dice cosas como " .. "CALMA" pbold .. " y, or = o " .. "Dicelo, no lo escupa." pbold $) +text
			soloer choosechar choice( "Intente no perder la calma" )choice: common2-meeting-talk
		else
			" 'Quien deberia intentar lidiar con el?" +text
			team-count 3 > if
				' common2-meeting-talk 2 #cyoa-team-choice
				choice( "Elige a alguien mas" )choice: common2-meeting-talk4
			else
				' common2-meeting-talk 3 #cyoa-team-choice
			then
		then
		vehicle.which@ 0 > if smartest charchoice( ": Tome un desvio" )choice: car-detour then
;

// Park Chatting

: common2-parkchat-tipme args( @ )
	<- @ 40 %chance if 
		.shooting++ 
	else
		50 %chance if 
			.mechanic++
		else
			.medical++
		then
	then
;

cyoa-choice: common2-parkchat-tips
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "de manera muy sabia y experimentada." $) text!
	else
		($ cname .. " ' intercambian consejos sobre diferentes temas." $) text!
	then
	cwits- if
		"El superviviente amistoso se confunde con el consejo inutil." +crtext badfx witsREV
	then;
	cwits+ if
		"El superviviente amistoso se ilumina con el consejo y da algunos grandes consejos a su vez!" +crtext
		goodfx ' common2-parkchat-tipme foreach-team-char witsREV
	then;
		($ "El superviviente amistoso aprecia eso y da un poco de consejo a " .. cname .. " 'a cambio." $) +crtext
		cchar common2-parkchat-tipme goodfx witsREV
;

cyoa-choice: common2-parkchat-pep
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "de manera reconfortante." $) text!
	else
		($ cname .. " 'intenta dar un discurso motivacional." $) text!
	then
	cattitude- if
		"El superviviente amistoso en realidad se siente peor ahora." +crtext badfx attitudeREV
	then;
	cattitude+ if
		"El superviviente amistoso se siente muy optimista despues de la charla, y da un discurso inspirador!" +crtext
		morale2+all goodfx attitudeREV
	then;
		($ "El superviviente amistoso da algunas palabras de consuelo a cambio." $) +crtext
		morale++all goodfx attitudeREV
;

cyoa-choice: common2-parkchat-bandit
	($ cname .. " 'Facilmente roba a la persona amigable. No tenia mucho, pero eso le ensenara una leccion importante en confiar en los demas." $) text! badfx
		2 4 rnd trunk-food+ 50 %chance if 1 3 rnd trunk-medical+ then rob-guilt
		aholeREV
;

cyoa-bridge: common2-parkchat-coolit
	cpet? if
		($ cname .. " comienza " .. "Farting" rbold .. " 'a la persona, y de alguna manera continua haciendolo durante varios minutos sin detenerse ni por un momento." $)
	else
		($ cname .. " 'Callate! No me importa lo que tengas que decir! Solo quiero que me ayudes a encontrar a mi hija!' " .. "Esperen que se enfrie." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Chico amable" recruitee .name!
		"El se asombro de esto. Afirma que esto esta en consonancia con las profecias, y que ha estado guardando un regalo para El Escogido!" +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Mira a " .. cname .. ", callado, hasta que se va. " .. thegroup .. " 'Como un dia normal" $) +crtext
		morale--all badfx
	then witsattREV 
;

cyoa: common2-parkchat
	"Como un dia normal" cyoa-title!
	($ Thegroup .. " 'se encuentra con otra persona que descansa en una banca de parque. Parece amable. Tienen una conversacion, igual que antes del fin del mundo.	Que fue el tema principal?" $) text!
		smartest charchoice( ": Intercambio de consejos" )choice: common2-parkchat-tips
		gallant charchoice( ": Palabra de animo" )choice: common2-parkchat-pep
		most-ahole if
			charchoice( ": Me estas dando tus cosas" )choice: common2-parkchat-bandit
		then
		most-grating if
			charchoice( ": Esperen que se calme" fcoolit )choice: common2-parkchat-coolit
		then
;

// Busted leg

cyoa-choice: common2-busted-med
	($ cname .. "  intenta tratar la pierna rota de la mujer." $) text!
	cmedical+ if
		($ cHe .. "  usa " .. chis .. " 'le da su medica a pronto para sanar rapidamente su pierna! Ella agradece el auxilio y ofrece lo que puede spare!" $) +crtext morale++ medicalREV medical++ goodfx -1 trunk-medical+ 2 trunk-food+
	then;
	cmedical- if
		($ cHe .. " no tiene ni idea de que " .. che .. " 'gasta un dia, y quizas hace cosas un poco peor." $) +crtext medicalREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx -1 trunk-medical+ 1 calendar-day+
	then;
	medicalroll if
		($ cHe .. " 'pasa un dia para ayudarla a tratar su pierna. Ella esta agradecida, pero no puede spare mucho!" $) +crtext morale++ medicalREV medical++ goodfx 2 trunk-food+
	else
		($ cHe .. " 'se esfuerza por ayudarlo durante un dia, pero no logra hacer mucho. Sin embargo, la practica hace perfecto." $) +crtext medicalREV medical++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp -1 trunk-medical+ 1 calendar-day+ heal-event
;

cyoa-bridge: common2-busted-coolit
	cpet? if
		($ cname .. " 'comienza a rodar en el frio suelo." $)
	else
		($ cname .. " 'le dice a la mujer que " .. "Esperen que se enfrie." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Mujer de palanca" recruitee .name!
		($ "Tomo esto como consejo que debia poner compresas frias en su pierna. Despues de " .. thegroup .. " 'le ayuda con ello, le ofrece un premio!" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Pregunta " .. "QUE HAY DE MAL CON TU, EXACTAMENTE?" pbold .. " 'se va en desprecio, arrastrandose con un pie." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-busted-bandit
	($ cname .. " 'facilmente roba a la mujer herida de lo poco que le quedaba de suministros. Bien hecho!" $) text! badfx
		2 4 rnd trunk-food+ rob-guilt aholeREV
;

cyoa: common2-busted
	"Pierna Rota" cyoa-title!
	($ Thegroup .. " 'se encuentra con una mujer cojeando con un baston. Una de sus piernas resulto herida en una caida reciente mientras huia de zombis." $) text!
		trunk-medical 0 > if
			medicalest charchoice( ": Ayudarle con la pierna" )choice: common2-busted-med
			choice( "No hay tiempo para ayudar" )choice
		else
			($ Thegroup .. " ': No tiene suministros medicos para ayudarla." $) +crtext
			choice( "Dejarle sola" )choice
		then
		most-grating if
			charchoice( ": Decirle que se COOL IT" fcoolit )choice: common2-busted-coolit
		then
		most-ahole if
			charchoice( ": Robarle sus suministros" )choice: common2-busted-bandit
		then
;

// Car trouble

: common2-carhelp-win
	homesearch1 ammosearch1
;

cyoa-choice: common2-carhelp-mech
	($ cname .. " 'intenta arreglar el coche averiado del hombre." $) text!
	cmechanic+ if
		($ cHe .. "  usa " .. chis .. " 'conocimientos de mecanica para repararlo rapidamente! Agradece la ayuda y da algunos suministros!" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	then;
	cmechanic- if
		($ cHe .. " 'tampoco sabe lo que " .. che .. " 'esta haciendo, y pierde el dia revoloteando." $) +crtext mechanicREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx 1 calendar-day+ heal-event
	then;
	mechanicroll if
		($ cHe .. " 'se toma un dia para ayudar a arreglar el coche del hombre. Da algunos suministros como recompensa!" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	else
		($ cHe .. " 'intenta ayudar durante un dia, pero no tiene exito. Aun asi, es una buena practica." $) +crtext mechanicREV mechanic++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp 1 calendar-day+ heal-event
;

cyoa-bridge: common2-carhelp-coolit
	cpet? if
		($ cname .. " 'ignora al hombre varado y comienza a comer hormigas que se arrastran por el suelo frio." $)
	else
		($ cname .. "  le dice al hombre varado que " .. "Esperen que se enfrie." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Hombre varado" recruitee .name!
		($ "ESTAS DERECHO! EL CARRO SOLO NECESITA MAS AGUA ACOTESADA PARA FUNCIONAR DE NUEVO!" pbold .. " Ofrece recompensa" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Empieza a gritar a " .. thegroup .. " se van " .. gthey .. " se van" .. g1s .. "." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-carhelp-bandit
	($ cname .. " roba facilmente las suministros del hombre varado, incluso su clave de torque." $) text! badfx
		common2-carhelp-win 1 WEAPON_WRENCH trunk.weapon+ rob-guilt aholeREV
;

cyoa: common2-carhelp
	"Ayudando con problemas mecanicos de coche" cyoa-title!
	char-gen to recruitee 
	0 recruitee .mechanical!
	0 recruitee .female! ricon
	WEAPON_WRENCH recruitee .weapona!
	($ Thegroup .. " se encuentra una persona intentando reparar su coche averiado. Claramente no sabe a que se dedica." $) text!
		mechanicest charchoice( ": Ayudame a arreglar el coche" )choice: common2-carhelp-mech
		most-charming if
			charchoice( ": Recrude a el" )choice: recruit-yes
		then
		most-grating if
			charchoice( ": Calmate!" fcoolit )choice: common2-carhelp-coolit
		then
		most-ahole if
			charchoice( "robar sus suministros" )choice: common2-carhelp-bandit
		then
		choice( "No hay tiempo para ayudar" )choice
;

// Bandit raid

: common2-raid-prize
	1 WEAPON_RIFLE trunk.weapon+ 20 trunk-rifle-shell+ 15 trunk-food+ morale++all goodfx
;

cyoa-choice: common2-raid-calmshot
	($ cname .. "  to shoot them from above. " .. che .. " 1" $) text! common2-raid-prize
;

: common2-raid-shoot+
	dup humany? 50 %chance and if .shooting++ then
;

: common2-raid-fightprize
	choosehuman alive? if
		"Los colonos se mantienen fieles a su palabra y dan una gran recompensa!" +crtext
		common2-raid-prize
		most-calmshot if
			choosechar ($ cname .. " hace unos tiros locos que inspiran a los demas." $) +crtext
			' .shooting++ foreach-team-char
		else
			' common2-raid-shoot+ foreach-team-char
		then
	then
;

cyoa-choice: common2-raid-fight
	someicon ($ Thegroup .. " 'decide ayudar a los colonos a pelear contra los bandidos, directamente." $) text!
	shootingREVall
	shooting-total 14 > if
		"La habilidad de tiro del grupo ayuda a los colonos a superar a los bandidos. Nadie resulta herido!" +crtext
		common2-raid-fightprize
	then;
	shooting-total 10 > if
		"La pelea es dura, pero las habilidades de tiro del grupo salvan el asentamiento." +crtext
		ouch--all bitefx common2-raid-fightprize
	then;
	shooting-total 6 > if
		"La pelea es brutal! Las habilidades de tiro del grupo apenas ayudan a ahuyentar a los bandidos." +crtext
		ouch12rnd--all bitefx common2-raid-fightprize
	then;
		"La ayuda prestada no fue suficiente en el tiroteo. Los bandidos obligan a todos a abandonar el asentamiento." +crtext ouch2-all bitefx
;

cyoa-choice: common2-raid-mech
	($ cname .. " 'trata de reforzar las defensas del asentamiento." $) text!
	cmechanic+ if
		($ cHe .. " 'rapidamente hace planes y reune a la gente para " .. chim .. " 'convertir el lugar en una fortaleza! Los bandidos son abrumados por las nuevas defensas cuando aparecen y huyen sin lastimar a nadie." $) +crtext mechanicREV mechanic++ common2-raid-prize
	then;
	mechanicroll if
		($ cHe .. " 'les dice a los colonos como construir fortificaciones en algunas areas debiles. Las defensas adicionales parecen ayudar mucho en el ataque de los bandidos! Los colonos estan agradecidos por la ayuda!" $) +crtext mechanicREV mechanic++ common2-raid-prize
	else
		($ cHe .. " 'trata de fortalecer algunas cosas, pero los esfuerzos no parecieron ayudar mucho contra el ataque de bandidos salvaje. Los colonos heridos acusan a " .. cname .. " 'de perder el tiempo, y se niegan a dar la recompensa." $) +crtext mechanicREV morale--all sting--all bitefx
	then
;

cyoa: common2-raid
	"Defensa contra el Asalto de Bandidos" cyoa-title!
	($ Thegroup .. " 'se les pide ayuda a un asentamiento. Ellos dicen que seran atacados por bandidos pronto. Si " .. thegroup .. " ' ayuda en la defensa, " .. gthey .. " ' recibira una recompensa de 15 comida, alguna municion y un rifle cowboy!	Esto podria ser " .. "very dangerous..." rbold $) text!
		vehicle.which@ 0 > if 
			choice( "No ayude, huya" )choice else choice( "No les ayude" )choice
		then
		choice( "Ayudenlos en tiroteo" )choice: common2-raid-fight
		mechanicest charchoice( ": Construya defensas" )choice: common2-raid-mech
		most-calmshot if
			charchoice( ": Lo manejo yo" )choice: common2-raid-calmshot
		then
;

// Rooftop relaxing

cyoa-choice: common2-relax-morale
	($ Thegroup .. " 'gasta algun tiempo " .. "esta solo pasando el rato" fbold .. "." $) text!
	morale2+all cool-it++ goodfx
;

cyoa-choice: common2-relax-strength
	($ Thegroup .. " 'obtiene algunas ejercicios de fuerza improvisados. La pieza de mobiliario mas pesada que se puede levantar es levantada, luego depositada. Repita hasta " .. "MEGABUFF" fbold .. "." $) text!
	' .strength++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-fitness
	($ Thegroup .. " 'obtiene algunas ejercicios cardiovasculares improvisados. Principalmente burpees, alguna salto de piernas. Repita hasta " .. "ULTRA FIT" fbold .. "." $) text!
	' .fitness++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-med
	($ cname .. " 'ensena " .. everyone .. " 'ensena que " .. che .. " 'sabe sobre el cuidado medico de emergencia y la dentisteria semipicosa." $) text!
		cmedical- if 
			($ cHe .. " 'no sabia mucho al respecto! Es una leccion completamente inutil." $) +crtext badfx
		else
			cchar ' .medical++ foreach-team-char-except goodfx
		then medicalREV morale++
;

cyoa-choice: common2-relax-mech
	($ cname .. " 'ensena " .. everyone .. " 'ensena que " .. che .. " 'sabe sobre la reparacion de automoviles y otras formas de averiarse accidentalmente las maquinas." $) text!
		cmechanic- if 
			($ cHe .. " 'no sabia mucho al respecto! Es una leccion completamente inutil." $) +crtext badfx
		else
			cchar ' .mechanic++ foreach-team-char-except goodfx
		then mechanicREV morale++
;

cyoa-choice: common2-roofrelax-reveal
	($ cname .. " 'descubre " .. chis .. " 'VERDADERO ESSENCIA." $) text!
		allREV goodfx
;

cyoa: common2-roofrelax
	image-tired-survivor 63 to cyoaicon
	($ Thegroup .. " 'obtiene un momento raro tranquilo para descansar en el tejado de un apartamento." $) text!
	solo if
		soloer choosechar
		($ "Sin nadie a molestar " .. chim .. ", " .. che .. " decide a:" $) +crtext
		choice( "CALMA" )choice: common2-relax-morale
		choice( "Ejercicio para la fuerza" )choice: common2-relax-strength
		choice( "Ejercicio para la fitness" )choice: common2-relax-fitness
	else
		"En boca cerrada no entran moscas" +crtext
		' common2-roofrelax-reveal cyoa-team-choice
	then
;

// Safehouse relaxing

cyoa: common2-saferelax
	image-door-on-brick 27 to cyoaicon
	($ Thegroup .. " 'Toma un momento de paz para descansar en un refugio, no hay zombies a la vista." $) text!
	solo if
		soloer choosechar
		($ "Sin nadie a molestar " .. chim .. ", " .. che .. " decide a:" $) +crtext
		choice( "CALMA" )choice: common2-relax-morale
		choice( "Ejercicio para la fuerza" )choice: common2-relax-strength
		choice( "Ejercicio para la fitness" )choice: common2-relax-fitness
	else
		"Con algo de tiempo libre, deciden:" +crtext
			50 %chance if 
				choice( "CALMA" )choice: common2-relax-morale
			else
				50 %chance if
					choice( "Ejercicio para la fuerza" )choice: common2-relax-strength
				else
					choice( "Ejercicio para la fitness" )choice: common2-relax-fitness
				then
			then
			mechanicest charchoice( "ensenar habilidades mecanicas" )choice: common2-relax-mech
			medicalest charchoice( "ensenar habilidades medicas" )choice: common2-relax-med
	then
;

} }
