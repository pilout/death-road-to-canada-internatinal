
cyoa{ road{ 


cyoa-choice: common3-molotov-2
	($ cname .. "  fabrique 2 cocktails Molotov avec 20 gaz! Soyez prudent avec ceux ci!" $) text! 2 WEAPON_MOLOTOV trunk.weapon+ -20 trunk-gas+
;

cyoa-choice: common3-molotov-4
	($ cname .. "  fabrique 4 cocktails Molotov avec 40 gaz! De quoi griller un tas de zombies!" $) text! 4 WEAPON_MOLOTOV trunk.weapon+ -40 trunk-gas+
;

cyoa-choice: common3-molotov-6
	($ cname .. "  fabrique 6 cocktails Molotov avec 60 gaz! Cela devrait faire une grande difference!" $) text! 6 WEAPON_MOLOTOV trunk.weapon+ -60 trunk-gas+
;

cyoa-choice: common3-molotov-bottle
	($ cname .. "  prend 6 bouteilles vides avec " .. chim .. ". Ils pourraient etre utiles ?" $) text! 6 WEAPON_BOTTLE trunk.weapon+
;

cyoa: common3-molotov
	smartest choosechar cicon "Fabrication de cocktails Molotov" cyoa-title!
	($ Thegroup .. "  trouve une maison saccagee avec des bouteilles et des chiffons en lambeaux. Cela semblait etre une perte de temps de la fouiller, mais " .. cname .. "  a une idee :" $) text!
	cchar .wits 2 > if
		($ gThey .. "  pourrait fabriquer des cocktails Molotov, si " .. gthey .. "  peut se passer dessence !" $) goodfx
	else
		($ gThey .. "  pourrait prendre les bouteilles avec " .. ghim .. ", pour les lancer sur les zombies et autres ennemis !" $) badfx
	then +crtext witsREV
		
		cchar .wits 2 > if
			trunk-gas 20 < if
				"'Malheureusement, il n'y a pas assez d'essence a perdre." +crtext
			then
			trunk-gas 19 > if
				choice( "Fabriquer 2 cocktails Molotov   20 essence" )choice: common3-molotov-2
			then
			trunk-gas 39 > if
				choice( "Fabriquer 4 cocktails Molotov   40 essence" )choice: common3-molotov-4
			then
			trunk-gas 59 > if
				choice( "Fabriquer 6 cocktails Molotov   60 essence" )choice: common3-molotov-6
			then
		else
			choice( "Prendre les bouteilles" )choice: common3-molotov-bottle
		then
		choice( "Quitter la maison" )choice
;

cyoa-choice: common3-pipe-go
	($ cname .. "  essaie de terminer de fabriquer les bombes." $) text!
	cmechanic+ if
		($ cHe .. "  termine facilement ces dernieres, et en finit avec 6 bombes a pipe." $) +crtext goodfx 6 WEAPON_PIPEBOMB trunk.weapon+
	else
		mechanicroll if
			($ cHe .. "  ne parvient pas a se faire exploser en partie grace au hasard " .. chim .. " arrive a conserver  bombes a pipe, en ruinant la moitie delles " .. cHe .. "  nest pas assez a laise avec ce genre de travail, et fait une erreur. Dommage" $) +crtext 3 WEAPON_PIPEBOMB trunk.weapon+ goodfx
		else
			($ cHe .. " ' ne se sent pas a l'aise pour du travail de cette nature, et commet une faute. Desole !" $) +crtext ouch2- cchar ' .ouch01rnd-- foreach-team-char-except bitefx
		then
	then mechanicREV alive? if mechanic++ then
	
;

cyoa: common3-pipe
	someicon " Zombie Bombeur" cyoa-title!
	($ Thegroup .. " ' decouvre un abri abandonne. Il y a un peu d'munition dedans." $) text!
	ammosearch1
	($ " En poursuivant la recherche, des bombes a pipe inachevees sont dans ledifice. Quelquun devait avoir du quitter en urgence. " .. " Travailler avec elles pourrait etre tres dangereux !" rbold $) +crtext
		choice( " Les laisser seul !" )choice
		mechanicest charchoice( ": Terminer de les fabriquer" )choice: common3-pipe-go
;


cyoa-choice: common3-bridgekeep-plow
	someicon
	($ Thegroup .. " il ne fait que lignorer et fonce droit dessus. " .. "PAF!" rbold $) text!
	car-plowthrough
;

cyoa-choice: common3-bridgekeep-rage
	($ cname .. " charges right at the sports gear armored " .. "PLASH!" fbold $) text! goodfx rageREV morale++
;

cyoa-bridge: common3-bridgekeep-push
	someicon
	($ Thegroup .. " sort de la voiture et tente de pousser le garde du pont hors du pont." $) text!
	strength-total fitness-total + 15 > if
		($ " Apres quelques difficultes, " .. thegroup .. "  est capable de le jeter du pont !" $) +crtext goodfx
	else
		($ "' C'est inutile ! L'homme en armure vient de frapper " .. thegroup .. "  avec ses gants de boxe ! " $) +crtext
			vehicle.which@ 0= if ($ gThey .. " courir" .. g1s .. " le passe apres la bagarre." $) +text then
		sting--all bitefx
		vehicle.which@ 0 > if choice( "Le rouler sur avec la voiture" )choice: common3-bridgekeep-plow then
		 
	then
	' .strengthREV foreach-team-char ' .fitnessREV foreach-team-char
;

: common3-bridgekeep-choice
	choice( "Le jeter du pont" )choice: common3-bridgekeep-push
	vehicle.which@ 0 > if choice( "Le rouler a travers avec la voiture" )choice: common3-bridgekeep-plow then
	most-angrystrong if
		charchoice( ": lutter contre lui a une!" )choice: common3-bridgekeep-rage
	then
;

cyoa-bridge: common3-bridgekeep-coolit
	cpet? if
		($ cname .. " marche jusquau bord du pont, se tourne et " .. "fait un pet" pbold .. " dans la direction generale du pont gardien." $)
	else
		($ cname .. " demeure a lextremite du pont et hurla au garde frontiere pour " .. "CALME TOI." pbold $)
	then text!
	cool-it++
	($ "'L'homme blinde continue de regarder droit devant. On ne sait pas s'il a meme entendu " .. cname .. "." $) +crtext badfx
	common3-bridgekeep-choice
;

cyoa: common3-bridgekeep
	"Gardien Silencieux" cyoa-title!
	($ "'Un homme vetu d'une armure de fortune, fabriquee a partir d'equipement sportif, monte la garde au milieu d'un etroit pont en bois sur la route. Il porte des gants de boxe, regarde droit devant lui la voiture, silencieux alors qu'il bloque le passage." $) text!
		common3-bridgekeep-choice
		most-grating if
			charchoice( ": Dites a la sentinelle de se calmer" )choice: common3-bridgekeep-coolit
		then
;

// Ranting raving ambush

cyoa-choice: common3-ranter-rage
	($ "Au lieu detre surpris par lembuscade, " .. cname .. " ' se precipite sur l'homme pendant qu'il delire et le met KO. Il n'a meme pas le temps de reagir." $) text! goodfx rageREV morale++
	most-ahole if choosechar
		($ cname .. "  saisit loccasion de voler ses affaires !" $) +crtext
		homesearch1 ammosearch1 rob-guilt2
	then
;

cyoa-choice: common3-ranter-obliv
	($ cname .. " ' n'accuse pas du tout l'homme qui fulmine. Il devient tellement confus par cela qu'il devient silencieux et s'en va. 	" .. cname .. " ' ne faisait pas attention tout le temps et est davantage attire par " .. chis .. "  son propre petit monde." $) text! goodfx witsattREV attitude++ wits--
;

cyoa-bridge: common3-ranter-coolit
	cpet? if
		($ cname .. " ' interrompt les diatribes de l'homme en continu " .. "PETE" rbold .. "  quand il essaie de parler." $)
	else
		($ cname .. " ' interrompt les diatribes de l'homme pour lui dire de " .. "'DIS LE, NE LE PULVERISE PAS !" pbold $)
	then text!
	cool-it-7 if
		($ "'Ha ! Tu es d'accord !" pbold .. "francais" $) +crtext
		cooled-it goodfx recruit-yesno morale3+all
	else cool-it++
		($ "Oh! On dirait quon a un perturbateur!" pbold .. "il explose " .. cname .. " avec un " .. "RANT DEVASTATEUR DE PERSPICACITE!" fbold .. " Le coup de gueule devastateur fait une petite onde de choc!" $) +crtext
		morale4- ouch-- badfx
	then witsattREV 
;

cyoa-choice: common3-ranter-charm
	($ cname .. " " $) text! 
	solo if
		($ cname .. " etait un bandit!" $)
	else
		"le groupe etait de bandits!"
	then  +text goodfx witsattREV morale++
	most-ahole if choosechar
		($ cname .. " lui montre cela " .. gtheyare .. " brigand" gaVSs .. "!" $) +crtext
		homesearch1 ammosearch1 rob-guilt2
	then
;

cyoa-choice: common3-ranter-fight ricon
	($ Thegroup .. " " $) text! bitefx ouch--all
;

cyoa-choice: common3-ranter-run ricon
	($ Thegroup .. "  damage " .. "RANTING SIZZLING" rbold .. " avant after  apres " .. thegroup .. "  le perd." $) text! badfx morale2-all
;

cyoa: common3-ranter
	"Ranting Man" cyoa-title!
	char-gen to recruitee "George" recruitee .name!
	26 recruitee .specialtype! ricon
	6 recruitee .wits! 0 recruitee .attitude! 0 recruitee .composure! 6 recruitee .loyalty!
	2 recruitee .morale! 6 recruitee .shooting! 6 recruitee .medical! 6 recruitee .mechanical!
	($ Thegroup .. " ' est pris en embuscade par un homme qui hurle. Il est en train de delirer et de s'emporter ! Il n'a pas l'air particulierement dangereux, mais ces coups de gueule commencent a " .. "siffler." pbold $) text!
		most-angrystrong if
			charchoice( ": Mettez le KO" )choice: common3-ranter-rage
		then
		most-grating if
			charchoice( "': DIS LE, NE LE PULVERISE PAS" fcoolit )choice: common3-ranter-coolit
		then
		most-oblivious if
			charchoice( ": Lignorer completement" )choice: common3-ranter-obliv
		then
		most-charming if
			charchoice( ": Ecoute le" )choice: common3-ranter-charm
		then
		choice( "Fuis !" )choice: common3-ranter-run
		choice( "Bats toi contre lui !" )choice: common3-ranter-fight
;

// BEE PROBLEMS

: swatsoreats cpet? if "mange" else "ecrase" then ;

cyoa-choice: common3-bee-swat
	($ cname .. " " .. swatsoreats .. "  labeille ! Cela resout le probleme de labeille, mais " .. che .. "  se fait piquer !" $) text!
	ouch-- temperREV bitefx
	alive? not if ($ cHis .. "  le sacrifice ne sera jamais oublie !" $) +crtext then
	
;

cyoa-choice: common3-bee-coolit
	cpet? if 
		($ cname .. "  setire et ca ressemble un peu a " .. "CALME TOI." pbold $)
	else 
		($ cname .. "  demande a la abeille de " .. "CALME TOI." pbold $)
	then text!
	($ "La abeille volle droit dans sa gueule  " .. cname .. "' il est pique ! " .. cHe .. "  ca fait mal beaucoup " .. " Totalement lignore !" rbold $) +crtext
	ouch-- witsattREV bitefx morale3- cool-it++
	alive? not if ($ cHis .. "  le sacrifice ne sera jamais oublie !" $) +crtext then
;

cyoa-choice: common3-bee-obliv
	($ cname .. "  ne sest pas apercu " .. cHe .. " ' exemple et ignore egalement " $) text!
	notsolo if ($ Everyone .. " suivra " .. chis .. "  la abeille" .. g2s .. "  elle." $) +text then
	"La abeille continue de bourdonner autour de la voiture des mois apres quelle a ete abandonnee." +crtext goodfx witsattREV
;

cyoa-choice: common3-bee-panic
	($ Thegroup .. " descends dans la panique! La voiture perd le controle et secrase contre un arbre." $) text!
	car-plowthrough
	"A un moment donne, labeille sest echappee par la fenetre." +crtext
;

cyoa-choice: common3-bee-beeman
	($ cname .. " " $) text!
	morale2+all goodfx
;

cyoa: common3-bee
	"Probleme dabeilles!" cyoa-title! angriest choosechar cicon
	vehicle.which@ 0= if 
		($ "Une abeille harcele " .. thegroup .. "il met une eternite a partir. Si tu avais une voiture, tu aurais surement ete protege des abeilles !" $) text! morale2-all
	then;
	($ "'Une abeille entre dans la voiture ! Meme avec une fenetre ouverte, elle ne s'en va pas. Elle continue de voler directement dans " .. thegroup .. "'s face" .. gs .. "! Cela pourrait etre le plus grand defi, a ce jour !" $) text!
		choice( "PANIQUE!" )choice: common3-bee-panic
		most-grating if
			charchoice( "  dit a labeille de SE CALMER" )choice: common3-bee-coolit
		then
		most-oblivious if
			charchoice( "  ignore labeille" )choice: common3-bee-obliv
		then
		angriest charchoice( " " .. swatsoreats .. "  labeille" )choice: common3-bee-swat
		20 stpick if
			charchoice( "  chante la chanson de labeille" )choice: common3-bee-beeman
		then
		
;

// Gloomy Weather

: common3-gloom-attcheck args( @ )
	<- @ .attitudeREV
	<- @ .cattitude+ if
		($ <- @ .colourname .. "  garde une attitude positive!" $) +crtext
	then;
	<- @ .cattitude- if
		<- @ .morale2-
	else
		<- @ .morale--
	then	
;

cyoa-choice: common3-gloomy-pick
	($ cname .. "  commence a parler, pour ameliorer lhumeur." $) text!
	cpet? if
		($ cHe .. " fait " .. cdogcat .. " ' bruits. C'est une distraction suffisante de la meteo!" $) +crtext morale++	goodfx
	then;
	cchar grating? if
		($ cHe .. "  raconte " .. everyone .. "  to " .. "  CALMER LE JEU." pbold $) +crtext
		cool-it-7 if
			($ Everyone .. "  rit de bon cur! Cette phrase daccroche ne vieillit jamais!" $) +text
			morale2+all goodfx
		else cool-it++
			"Cela ne passe PAS bien du tout." +text morale3-all badfx
		then witsattREV
	then;
	ctemper- if
		($ cHe .. " perd " .. chis .. "  temperament et commence a se plaindre de la meteo! " .. cHe .. "  se sent mieux mais " .. everyone .. "  il doit supporter les plaintes." $) +crtext badfx
		temperREV morale++ cchar ' .morale-- foreach-team-char-except
	then;
	cattitude+ if
		($ cHe .. "  parvient a distraire " .. everyone .. "  de la meteo deplaisante !" $) +crtext goodfx attitudeREV
	then;
	cattitude- if
		($ cHe .. "  plaindrait et plaindrait, sur la meteo et dautres sujets non lies." $) +crtext badfx
		attitudeREV morale2-all
	then;
	"'Cela ne laide pas beaucoup." +crtext badfx
	' common3-gloom-attcheck foreach-team-char
;

cyoa: common3-gloomy
	"Voyage Meteorologique Triste" cyoa-title! someicon
	($ "'La meteo est grise et sales et ca commence a me " .. thegroup .. "  deprime. " $) text!
	solo if
		($ cHe .. "  na personne a plaindre de la meteo a." $) +text
		soloer common3-gloom-attcheck
	else
		"Quelquun decide de parler, pour ameliorer lambiance:" +crtext
		' common3-gloomy-pick cyoa-team-choice
	then 0.4 1 frnd weather! -1 1 frnd wind_force!

;

// Fart Mystery


cyoa-choice: common3-fart-blame
	($ cname .. "  est accuse du flatulence." $) text!
	cpet? if
		($ "Ceci est une mauvaise " .. cdogcat .. "!" $) +crtext morale-- badfx
	then;
	cattitude+ if ($ cHe .. "  hausse les epaules avec bonhomie." $) +crtext attitudeREV else morale2- then
	"Lodeur persiste !" +crtext
	cchar ' .morale-- foreach-team-char-except badfx
	"Was this the real culprit? Who can say..." +crtext
;

cyoa-choice: common3-fart-coolit
	cpet? if
		($ cname .. "  emet un " .. "PET TRES CLAIR SECOND PET." rbold $) text!
		"Mystere resolu !" +crtext
	else
		($ cname .. "  raconte " .. everyone .. "  to " .. "CALME TOI." pbold $) text!
		($ "PERLES DE SUEUR NERVEUSE" rbold .. "  degoulinent sur " .. cname .. "'le visage. Pourtant rien ne peut etre prouve !" $) +crtext
	then cool-it++ cchar ' .morale3- foreach-team-char-except witsattREV badfx
;

cyoa-choice: common3-fart-smart
	notcchar to victim
	($ cname .. "  explique une sequence de deduction compliquee qui aboutit a une conclusion:" $) text!
	cattitude+ if
	($ "Le pet a ete fait par un " .. "GHOST!!! WWWWHHHHOAAAAAAAAA!" fbold $) +crtext witsattREV morale++all goodfx
	then;
	($ "Cetait " .. vname .. "  qui etait le prouteux !" $) +crtext victim .morale2- badfx
	cloyalty- if
		($ "En secret, cetait en realite " .. cname .. " qui etait le " .. "le monstre a flatulences!" rbold $) +crtext
		loyaltyREV
	then witsREV morale++
;

cyoa: common3-fart
	"Le Mystere du Fantome Fart" cyoa-title!
	image-bad-smell 33 to cyoaicon
	($ "C'est juste une journee typique sur la Route de la Mort quand quelqu'un " .. "fait des flatulences dans la voiture." rbold $) text!
	solo if
		"Qui en a pu faire ? Nous pourrions jamais le savoir a nouveau." +crtext 1 trunk-gas+ badfx
	then;
	solohuman if choosepet
		($ "Qui en a pu faire ? Cetait probablement " .. cname .. ". Quel mauvais " .. cdogcat .. "!" $) +crtext morale-- badfx
	then;
		"La drame et la mystere absorbent lequipe.		Choisissez quelquun a blamer:" +crtext
		most-grating if
			charchoice( "  raconte " .. everyone .. " lui pour le REFROIDIR" )choice: common3-fart-coolit
		then
		smartest-pass if 
			smartest charchoice( " lhomme resout le mystere" )choice: common3-fart-smart
		then
		' common3-fart-blame cyoa-team-choice
;

// Alligator

: common3-gator-hotwire
	mechanicest choosechar cmechanic+ if cicon
		($ cname .. " lhomme parvient a voler la voiture!" $) +crtext mechanicREV goodfx
	else
		($ "Sans un bon mecanicien, il n'y a pas d'autre moyen pour " .. thegroup .. " demarrer la voiture!" $) +crtext
		car-abandon 1 to gator-ate-key
	then
;

cyoa-choice: common3-gator-str
	($ cname .. " lutte contre lalligator pour les cles !" $) text!
	strength+roll if
		($ "Lalligator est vaincu par " .. cname .. "'sa force brute ! Il recrache les cles." $) +crtext goodfx strength++
	else
		($ "Lalligator gagne et finit par prendre une enorme bouchee de " .. cname .. " ! Il laisse tomber les cles." $) +crtext ouch2- bitefx
	then alive? if strengthREV then
;

cyoa-choice: common3-gator-fit
	($ cname .. " embete lalligator en le poursuivant et en le fuyant, dans lespoir quil laisse tomber les cles." $) text!
	fitness+roll if
		($ "Lalligator est fatigue par " .. cname .. "'energie ! Les cles finissent par lui echapper de la bouche." $) +crtext goodfx fitness++
	else
		($ "Lalligator se precipite et mord " .. cname .. "  quand " .. che .. " se fatigue ! Lalligator laisse tomber les cles." $) +crtext ouch2- bitefx
	then alive? if fitnessREV then
;

cyoa-choice: common3-gator-shoot
	($ cname .. " tire sur lalligator !" $) text!
	shooting+roll if
		($ "Lalligator est dynamite par " .. cname .. "''il a un bon abattage! Les cles de voiture tombent." $) +crtext goodfx shooting++
		shootingREV
	else
		($ cname .. "  rate le tir. Lalligatoravalide les cles et sen va roulant." $) +crtext
		shootingREV common3-gator-hotwire
	then
;

cyoa-choice: common3-gator-obliv
	($ cname .. " ' prend simplement les cles de voiture droit dans la bouche de l'alligator! Pas de probleme !	L'alligator n'y attendait rien du tout. Il est si surpris qu'il n'a pas referme ses machoires " .. Thegroup .. "  conduit la voiture!" $) text! goodfx witsattREV
;

cyoa-choice: common3-gator-mech
	someicon
	($ Thegroup .. " ' juge que les cles de voiture ne valent pas la peine de se meler a un alligator extremement dangereux.	L'alligator est triste que sa strategie ne fonctionne pas, et perd un point de moralite."  $) text!
	common3-gator-hotwire
;

cyoa: common3-gator
	"Cles egarees" cyoa-title!
	($ Thegroup .. " ' ne trouve plus les cles de voiture ! Apres un moment " .. gthey .. "  repere" .. g1s .. "  les cles qui gisent dans la bouche dun alligator ouvert " .. "'gueule ouverte du gator" rbold .. "'.	Apres que la plupart des humains ont disparu, les alligators ont commence a se repandre sur toute la cote est. Il est rumeur qu'ils mangent des zombies et des cles de voiture." $) text!
		strongest charchoice( "  lutte contre lalligator" )choice: common3-gator-str
		fittest charchoice( "  epuise lalligator" )choice: common3-gator-fit
		most-oblivious if
			charchoice( "        " )choice: common3-gator-obliv
		then
		roadtrip-has-firearm? if
			shootest charchoice( " tire sur lalligator" )choice: common3-gator-shoot
		then
		choice( "Abandonner les cles de la voiture" )choice: common3-gator-mech
;

// Trap Town

cyoa-choice: common3-traptown-pick
	($ cname .. " his ability " .. chis .. " capacite." $) text!
	cchar paranoid? if
		($ cHe .. " ' est extremement paranoiaque a chaque pas et action. C'est lent, de nombreux pieges se declenchent. " .. cname .. "  est pret pour chacun deux et evite les blessures." $) +crtext goodfx witsattREV mechanic++ ammosearch2 homesearch2
	then;
	cchar oblivious? if
		($ cHe .. "  fonce a travers. La plupart des pieges frappent " .. chim .. ". " .. cname .. "  survit en quelque sorte a ce long processus !" $) +crtext bitefx witsattREV sting2- morale2-
		"Voir autant de pieges en action etait educatif !" +crtext
		' .mechanic++ foreach-team-char ammosearch2 homesearch2
	then;
	wits+roll if
		($ cHe .. "  fait attention a lendroit ou " .. che .. "' va. De nombreux pieges sont reperes et declenches en toute securite. " .. cname .. "  evite les blessures !" $) +crtext goodfx witsREV mechanic++ ammosearch2 homesearch2
	then;
	mechanic+roll if
		($ cHe .. " utilise " .. chis .. "  experience avec des trucs mecaniques pour raisonner ou " .. che .. "  mettrait des pieges si " .. che .. "  etait une sorte de cingle. " .. cname .. "  evite ou desarme de nombreux pieges de cette facon !" $) +crtext goodfx mechanicREV mechanic++ ammosearch2 homesearch2
	then;
		($ cHis .. " ' la capacite d'eviter les pieges n'est pas si grande. " .. cname .. "  se fait dechiqueter mal " $) +crtext
		witsREV mechanicREV ouch2- bitefx
;

cyoa-bridge: common3-traptown-pick4
	someicon
	"Qui devrait explorer la ville piegee?" cyoa-text!
	' common3-traptown-pick cyoa-team-choice
;

cyoa: common3-traptown
	"Ville de Pieges" cyoa-title!
	($ Thegroup .. "  trouve une rue dans une ville qui est couverte de pieges. Trappes recouvertes de couvertures. Des pieges partout. Des barils a gros cailloux. Et ces sont juste les visibles." $) text!
		solo if
			($ "Rechercher cette zone pour du butin peut etre " .. "tres dangereux" rbold .. ", particulierement depuis quil est seul " .. soloname .. " est seul!" $) +crtext
			soloer charchoice( " essaye dexplorer" )choice: common3-traptown-pick
		else
			"Envoyer un scout explorer pour du butin?" +crtext
			team-count 3 > if
				' common3-traptown-pick 2 #cyoa-team-choice
				choice( "Choisissez quelquun dautre" )choice: common3-traptown-pick4
			else
				' common3-traptown-pick 3 #cyoa-team-choice
			then
		then
		choice( "Fuyez!" )choice
		
;

// Creepy Store

cyoa-choice: common3-creepy-go
	($ cname .. " entre dans le magasin sombre et crepusculaire." $) text!
	cchar paranoid? if
		($ cHis .. " le sens habituel de la paranoia se retourne contre vous! " .. cHe .. " sa sens de lalerte normale revient en arriere  " .. cname .. " voit une souris minuscule et alors commence a " .. " hurlant comme un Youtuber" fbold .. "!" $) +crtext morale2- witsattREV sfx{ creepy }
	then;
	cchar oblivious? if
		($ cHe .. " ' ne remarque rien d'effrayant la dedans. Pas meme quand un " .. " SQUELETTE DANSANT EFFRAYANT" fbold .. "  tombe du plafond et commence a cliqueter !" $) +crtext
		witsattREV homesearch2 goodfx
	then;
	temper+roll if
		($ cHe .. "  reste calme, prend le butin " .. che .. "  trouve, et sort de la !" $) +crtext
		temperREV homesearch2 goodfx
	then;
		($ cHe .. " perd " .. chis .. "  calme et sort de la en courant, affirmant quil ny avait pas de butin !" $) +crtext
		temperREV morale-- badfx
;

cyoa-choice: common3-creepy-allgo
	someicon ($ Thegroup .. " entre dans le magasin sombre et crepusculaire." $) text!
	angriest-pass if angriest choosechar cicon
		($ cname .. "  prend un mannequin pour un zombie et attaque ! Cela fait " .. everyone .. "  paniquer et commencer aussi a attaquer aveuglement. Au final, aucun mannequin nest reste debout." $) +crtext temperREV
		ouch--all bitefx
	else
		"Tout le monde reste assez calme ! Le magasin est pille sans incident." +crtext goodfx
	then ' .temperREV foreach-team-char homesearch2
;

cyoa-choice: common3-creepy-pick
	"Qui devrait entrer dans le magasin effrayant ?" text!
	' common3-creepy-go cyoa-team-choice
;

cyoa: common3-creepy
	"Magasin sombre et effrayant" cyoa-title! image-female-zombie-1
	($ Thegroup .. " ' trouve un magasin inonde. Il y a beaucoup de mannequins a l'interieur, a peine visibles dans l'obscurite. L'ensemble du magasin est particulierement effrayant. Il peut y avoir des fournitures moisies laissees la dedans." $) text!
		choice( "Francais" )choice
		solo if
			soloer charchoice( " entre dans le magasin" )choice: common3-creepy-go
		else
			choice( "Envoie quelquun" )choice: common3-creepy-pick
			choice( "Envoie tout le monde !" )choice: common3-creepy-allgo
		then
;

// Bad Weather Siege

cyoa-choice: common3-badweather-drive
	smartest choosechar
	($ Thegroup .. " fait preuve dimprudence et decide de continuer a conduire." $) text!
	cchar .wits 3 skillcheck? if 
		($ cname .. " se tend " .. chis .. " les yeux et parvient a eviter de justesse de percuter quoi que ce soit !" $) +crtext goodfx witsREV
	else
		car-armor 0 > if
			"La voiture percute des obstacles invisibles sous la pluie. Une partie de son blindage finit par tomber." +cr +text goodfx
			-1 ' car-armor +!
		else
			-2 -3 rnd car-chassis+ badfx
			car-chassis 0 > if
				($ "La voiture est endommagee en traversant une epave " .. thegroup .. " 'n'a pas vu a temps, mais elle roule toujours." $) +crtext
			else
				($ "La voiture percute de plein fouet le cote dune voiture en ruine sur la route et est detruite " .. Thegroup .. " commence a marcher." $) +crtext
			then 
		then
	then
;

cyoa-choice: common3-badweather-fight
	($ Thegroup .. " 'ne s'installe meme pas dans cette maison avant de realiser qu'une grande horde s'approche sous la pluie battante." $) text!
	2.3 15 20 rnd 1 siegecast // 1.7
	"events/siege/region/midopen1.df" $load-region
	choice( "Essayez de survivre" )choice: premission
;

cyoa-choice: common3-badweather-camp
	someicon ($ Thegroup .. " prend la route secondaire moins frequentee et trouve un endroit pour camper jusqua ce que le temps seclaircisse." $) text! chowday
	"Il faut une journee supplementaire pour que le temps sameliore suffisamment pour continuer le voyage " +crtext
	+cr fullcamp heal-event 1 calendar-day+
;

cyoa: common3-badweather
	"Conduire par mauvais temps" cyoa-title!
	image-water-driving 73 to cyoaicon
	($ "La pluie commence a tomber. Il est difficile de voir, et les formes des zombies rodent juste a la limite de l'obscurite " .. Thegroup .. " ' n'est pas sur de ce qui serait pire : continuer, ou s'arreter ici." $) text!
	vehicle.which@ 0 > if choice( "Continuez a conduire" )choice: common3-badweather-drive then
	choice( "Cherchez refuge dans la maison" )choice: common3-badweather-fight
	fullmeal if
		choice( "Descendez la route secondaire, campez" )choice: common3-badweather-camp
	then 1.4 2 frnd weather! -2 2 frnd wind_force!
;

cyoa-choice: common3-horde-plow
	($ Thegroup .. "  decide dessayer de traverser la horde !" $) text! car-plowthrough
;

cyoa-choice: common3-horde-fight
	($ Thegroup .. "  se cache dans une maison voisine pour attendre que la horde se disperse. Les zombies finissent par sagiter et ils attaquent !" $) text!
	2.4 15 20 rnd 1 siegecast // 1.7
	"events/siege/region/midopen1.df" $load-region
	choice( "Essayez de survivre" )choice: premission
;

cyoa: common3-horde
	"Trafic intense" cyoa-title!
	image-zombie-hoard-1 9 to cyoaicon
	($ "'Il y a une grande horde de zombies sur la route devant. " .. Thegroup .. " ' n'est pas sur si " .. gthey .. "  pourrait les traverser ou non." $) text!
	choice( "Cherchez refuge dans la maison" )choice: common3-horde-fight
	vehicle.which@ 0 > if 
		choice( "Faire un detour" )choice: car-detour
		choice( "Labourez !" )choice: common3-horde-plow 
	then
;


// Blank
(*
cyoa-choice: common3--

;

cyoa: common3-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)
} }
