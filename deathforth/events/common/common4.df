
(*

- ouija board event "Weegee"
	- Pick a question to ask
	- Different if solo
		- AM I PRETTY? [yes] Morale Boost
		- WHERE'S THE LOOT? Shows hidden supplies nearby
		- 

	
*)

cyoa{ road{ 

// Not a Ouija Board

: weegeetm .. "Weegee Board [tm]" pbold .. ;
: weegeecurse args( char ) 13 <- char .seed! ;

: weegee-end
	($ Thegroup .. " now feels incredibly unlucky. Beware the " .. "Curse of Weegee!" rbold $) +crtext
	"LUCK MASSIVELY DECREASED!" fbold +crtext
	"Somewhere in the distance, a goblin laughs." pbold +crtext goblin-laugh
	
;

cyoa-choice: common4-weegee-pretty
	choosehuman cicon
	($ "The " weegeetm " spells out that " .. cname .. " is " .. "
	V E R Y P R E T T Y" pbold $) text!
	($ "Nice! " .. cname .. " Doesn't feel cursed at all by this delightful Weegee." $) +crtext
	morale2+
	"Somewhere in the distance, a goblin laughs, but in a nice way. As if to say 'hey.... what's up. have a nice day.'" pbold +crtext goblin-laugh
	choice( "nice" )choice
;

cyoa-choice: common4-weegee-loot
	($ "The " weegeetm " spells out the location of a hidden stash of loot in the house!" $) text!
	homesearch1
	weegee-end
;

cyoa-choice: common4-weegee-check
	weaselest choosechar cicon
	($ "When asked who will betray the group, the " weegeetm " spells out " $) text!
	weaselest-pass if
		($ cname .. "'s name!" $) +text
		loyaltyREV
		weegee-end
	else
		"Y O U R B U T T" pbold +crtext
		($ "What could this mean? The group becomes uneasy, and doubt festers. Could this be the " .. "Curse of Weegee?" rbold $) +crtext
		morale--all goblin-laugh
	then
;

cyoa-choice: common4-weegee-canada
	($ "The " weegeetm " points to the word " .. "NO" pbold .. " with amazing speed. Then it points to " .. "NO" fbold .. " again, while somehow catching on fire." $) text!
	($ "Whatever! These things are always wrong, anyway! " .. cname .. " throws the flaming " weegeetm " across the room. This makes " .. chim .. " also catch on fire." $) +crtext
	cchar isfireproof? if 
		($ cname .. " loves being fireproof!" $) +crtext
		morale++ goodfx
	else
		ouch-- alive? if bitefx then
	then
	alive? if
		"It was good exercise, at least." +crtext fitness++
	else
		($ "Don't mess with the " .. "Curse of Weegee!" rbold $) +crtext goblin-laugh
	then
	
;

cyoa: common4-weegee
	"Weegee Board" cyoa-title! choosehuman cicon
	($ Thegroup .. " is poking through an abandoned house and finds a " weegeetm " on the kitchen table. It's a common game used to communicate with " .. "GHOSTS" fbold $) text!
		choice( "Leave this NONSENSE alone" )choice
		($ "These things are rumored to be " .. "INCREDIBLY CURSED WITH BAD LUCK" rbold .. ", so beware! Still, it may be worth asking it a question..." $) +crtext
		solohuman if
			choice( "AM I PRETTY?" )choice: common4-weegee-pretty
		else
			choice( "WHO WILL BETRAY THE TEAM?" )choice: common4-weegee-check
		then
			choice( "WILL" .. I-or-WE .. "MAKE IT TO CANADA?" )choice: common4-weegee-canada
			choice( "WHERE'S THE LOOT?" )choice: common4-weegee-loot
;

// Dog Poop

: common4-dogpoo-trueend 16 trunk-food+ goblin-laugh ;
: common4-dogpoo-reward 16 trunk-food+ badfx ;

cyoa-choice: common4-dogpoo-ignore
	($ Thegroup .. " ignores the supply box and all the food inside of it.
	" .. gThey .. " find" .. g1s .. " a couple cans of food while searching the rest of the house, but must wonder what " .. gthey .. " missed." $) text!
	2 trunk-food+ goodfx
;

cyoa-choice: common4-dogpoo-pick

	cdog? if
		($ "The dog poop mysteriously disappears before " .. cname .. " nudges the box open!
		" .. cname .. " looks really guilty." $) text!
		morale--
		common4-dogpoo-reward
	then;
	ccat? if
		($ cname .. " seems especially angry that it had to bat the dog poop out of the way to open the box. " .. cHe .. " really feels like mauling something, moreso than usual." $) text!
		morale4-
		common4-dogpoo-reward
	then;
	cpet? if
		($ cname .. " kicks the supply box open. As an animal, " .. che .. " really doesn't care either way for dog poo, so it wasn't much of an obstacle." $) text!
		16 trunk-food+ goodfx
	then;
	dogpal? if
		($ cname .. " loves dogs, and is no stranger to picking up dog poop and putting it in a plastic bag.
		In fact, doing so reminds " .. cname .. " of civilization. Feeling that warmth in your hand, feeling alive!" $) text!
		morale2+ 16 trunk-food+ goodfx
	then;
	cchar oblivious? if
	($ cname .. " opens up the supply box. What was the big deal?
		" .. cHe .. " never noticed the dog poo." $) text!
		solo if
			goodfx
		else
			($ Everyone .. " is grossed out!" $) +crtext cchar morale2-all-except badfx
		then 16 trunk-food+
	then;
	($ cname .. " tries to nudge the poo off the box with a piece of wood, but it goes HORRIBLY wrong. " .. cHe .. " gets grossed out pretty bad!" $) text!
		cattitude+ if
			badfx
			($ cHe .. " attempts to keep up a positive attitude. One day, " .. cname .. " will be able to look at this and laugh. But not today." $)
		else
			($ "This is, by far, the worst thing to happen to " .. cname .. " in the zombie apocalypse. " .. cHe .. " will never, ever be the same." $) +crtext
			($ cname .. ":" pbold .. " PERMANENT MORALE PENALTY!" fbold $)
			-1 STAT_MORALE cchar .bonus+ sfx{ creepy }
		then +crtext morale2- 16 trunk-food+
	
;

cyoa-bridge: common4-dogpoo-stick
	"YOU WIN" cyoa-title! end-music
	"Every child knows that the only surefire way to move a piece of dog poo is to use a stick from a tree branch. It's nature's perfect poop remover.
	You channeled your sense of whimsy and child-like wonder to triumph over this dangerous situation." text!
	"Congratulations!" rainbold +crtext
	"YOU'VE FOUND THE TRUE ENDING TO DEATH ROAD TO CANADA!" fbold +crtext
	choice( "I did it!" )choice: common4-dogpoo-trueend
;

cyoa-bridge: common4-dogpoo-open
	solo if
		soloer choosechar common4-dogpoo-pick
	then;
	($ "Who should open the crate?" .. "
		WARNING: THIS IS REALLY GROSS
		THERE IS A DOG POO ON THE CRATE" rbold .. "
		THIS COULD BE A BIG DEAL" fbold $) text!
	' common4-dogpoo-pick cyoa-team-choice
;

cyoa: common4-dogpoo
	"TRUE DESPAIR" cyoa-title! picon!
	($ "As " .. thegroup .. " searches an abandoned house, a miracle happens. " .. gThey .. " find" .. g1s .. " a government ration box, unopened." $) text!
	($ "To " .. ghis .. " HORROR" rbold .. ", it turns out there is a " .. "dog poop" fbold .. " sitting " .. "RIGHT ON THE HANDLE" rbold .. " of the supply box." .. " There is no way to open it without touching the poo." rbold $) +crtext
	WEAPON_TREEBRANCH roadtrip-has-weapon? not if
		"This could be the biggest challenge, yet!" +crtext
	then
		choice( "No Way, Gross, Ignore It" )choice: common4-dogpoo-ignore
		solo if
			soloer charchoice( " opens it" )choice: common4-dogpoo-open
		else
			choice( "Have someone open it" )choice: common4-dogpoo-open
		then
		WEAPON_TREEBRANCH roadtrip-has-weapon? if
			choice( "Use tree branch to open it" )choice: common4-dogpoo-stick
		then
;

// Losing a sneeze

cyoa-choice: common4-sneeze-try
	($ cname .. " can't sneeze on command. " .. cHe .. " loses the sneeze! It's very annoying!" $) text!
	morale--
	cattitude- if
		($ "This is the " .. "worst thing to happen to ANYONE" rbold .. " in the zombocaylpse!" $) +crtext morale-- attitudeREV
	then
;

cyoa-choice: common4-sneeze-smash
	bitefx
	cchar .trait@ "BERSERK!" $= if
		($ cname .. " screams " .. "BERSERKEERRR!" fbold .. " and then punches " .. chim .. "self in the nose, as hard as possible. No self preservation instinct at all." $) text!
		ouch--
	then;
	($ cname .. " smashes " .. chis .. " own nose on a wall." $) text!
	sting--
	($ "It's an old trick! The blinding pain makes " .. cname .. " forget about the lost sneeze." $) +crtext
;

cyoa-choice: common4-sneeze-animal
	cchar to victim
	somepet if choosechar then
	($ vname .. " really stuffs " .. vhis .. " nose on " .. cname .. " and snorts.
	This coaxes out a good sneeze!" $) text! goodfx
;

cyoa-choice: common4-sneeze-spicy
	($ cname .. " opens up a bag of super spicy chips. " .. cHe .. " takes a big sniff." $) text!
	($ cname .. " accidentally sneezes into the bag! 
	" .. cHe .. " may as well eat the entire bag of chips now." $) +crtext
	-2 trunk-food+
	morale++
;

cyoa: common4-sneeze
	"THE LOST SNEEZE" cyoa-title! choosehuman cicon
	($ cname .. " really feels that " .. che .. " has to sneeze. This feeling drags on, but there's no sneeze.
	This may turn into one of those times a sneeze is lost! Combined with the pressures of the zombocalypse, this could break " .. cname .. "'s morale!" $) text!
		choice( "Try to sneeze" )choice: common4-sneeze-try
		choice( "Smash self on nose" )choice: common4-sneeze-smash
		numpets 0 > if choice( "Snort animal dander" )choice: common4-sneeze-animal then
		trunk-food 1 > if choice( "Snort spicy food - Costs 2 food" )choice: common4-sneeze-spicy then
;

// Game Bronus Entertainment System

: common4-gamebro-ender
	($ "This game is way too difficult! " .. "AAAAAAAAAARGH!" fbold $) +crtext
	($ cname .. " throws the GameBronus hard, destroying it." $) +crtext
	cattitude- if morale3- attitudeREV else morale2- then badfx
;

cyoa-choice: common4-gamebro-shoot
	($ "Drobnus Squadron" bbold .. " is a game about Drobnus blasting aliens with a machinegun. Most deaths happen due to bad jumping controls mixed with instant-kill pits." $) text!
	shooting++
	common4-gamebro-ender
;

cyoa-choice: common4-gamebro-med
	($ "Dr. Drobnus Deluxe" bbold .. " is an overly detailed surgery sim, rendered in 8 bit graphics. Most players lose by failing proper sterile surgical gloves procedure." $) text!
	medical++
	common4-gamebro-ender
;

cyoa-choice: common4-gamebro-mech
	($ "Drobnus Kart" bbold .. " is a racing game starring the popular character Drobnus and all his pals. Even if you're in first place, you can get hit by a Homing Toilet and end up in last place." $) text!
	mechanic++
	common4-gamebro-ender
;

cyoa-bridge: common4-gamebro-med-pick
	solohuman if
		choosehuman common4-gamebro-med
	then;
	($ "Who should get to play " .. "Dr. Drobnus Deluxe" bbold .. "?" $) cyoa-text!
	"WARNING: It's going to be rough on morale!" rbold +crtext
	' common4-gamebro-med cyoa-human-choice
;

cyoa-bridge: common4-gamebro-mech-pick
	solohuman if
		choosehuman common4-gamebro-mech
	then;
	($ "Who should get to play " .. "Drobnus Kart" bbold .. "?" $) cyoa-text!
	"WARNING: It's going to be rough on morale!" rbold +crtext
	' common4-gamebro-mech cyoa-human-choice
;

cyoa-bridge: common4-gamebro-shoot-pick
	solohuman if
		choosehuman common4-gamebro-shoot
	then;
	($ "Who should get to play " .. "Drobnus Squadron" bbold .. "?" $) cyoa-text!
	"WARNING: It's going to be rough on morale!" rbold +crtext
	' common4-gamebro-shoot cyoa-human-choice
;

cyoa: common4-gamebro
	"GameBronus Entertainment System" cyoa-title! someicon
	($ Thegroup .. " finds an old " .. "GameBronus [TM]" bbold .. "
	It's a handheld videogame system that survived this long because it's built like a brick.
	There were a few games with it, all known for their " .. "extremely frustrating difficulty:" rbold $) text!
		choice( "Leave GameBronus behind" )choice
		choice( "Dr. Drobnus Deluxe" )choice: common4-gamebro-med-pick
		choice( "Drobnus Kart" )choice: common4-gamebro-mech-pick
		choice( "Drobnus Squadron" )choice: common4-gamebro-shoot-pick
;

// Baked Beans on Garbage Mountain

: common4-beans-ender
	sting--
	($ "Garbage gets all over " .. cname .. "! It was worth it! It was all for the beans!" $) +crtext
	morale2- 3 trunk-food+ badfx
	
;

cyoa-choice: common4-beans-eat
	($ cname .. " eats some of the garbage pile! This jostles the can of beans at the top loose, without the whole pile coming down on " .. chim .. ".
	What a delicious solution!" $) text!
	morale2+ 3 trunk-food+ goodfx
;

cyoa-choice: common4-beans-smash
	($ cname .. " smashes the garbage pile! It collapses right onto " .. chim .. "." $) text!
	strength++ common4-beans-ender
;

cyoa-choice: common4-beans-climb
	($ cname .. " climbs the garbage pile! It collapses with " .. chim .. " on it." $) text!
	fitness++ common4-beans-ender
;

cyoa-bridge: common4-beans-smash-pick
	solohuman if
		choosehuman common4-beans-smash
	then;
	($ "Who should fight the garbage pile?" $) cyoa-text!
	' common4-beans-smash cyoa-human-choice
;

cyoa-bridge: common4-beans-climb-pick
	solo if
		soloer choosechar common4-beans-climb
	then;
	($ "Who should climb the garbage pile?" $) cyoa-text!
	' common4-beans-climb cyoa-team-choice
;

cyoa-bridge: common4-beans-eat-pick
	solopet if
		choosepet common4-beans-eat
	then;
	($ "Who should eat the garbage pile?" $) cyoa-text!
	' common4-beans-eat cyoa-pet-choice
;

cyoa: common4-beans
	"The Battle of Garbage Mountain" cyoa-title! someicon
	($ Thegroup .. " spots an extra tall pile of garbage near some buildings. It's maybe over 12 feet tall. Littering got pretty bad near the collapse of civilization.
	An " .. "Extra Large Can O' Baked Beans" rbold .. " is stuck near the top of the pile. It shines like a beacon of hope." $) text!
		choice( "Leave it alone" )choice
		choice( "Smash garbage pile" )choice: common4-beans-smash-pick
		choice( "Climb garbage pile" )choice: common4-beans-climb-pick
		numpets 0 > if choice( "Eat garbage pile" )choice: common4-beans-eat-pick then
;

// Old VHS tape

: common4-vhs-end
	morale++all
	($ "The TV shorts out after the movie, and is left behind." +crtext
;

: sumo-vhs args( char )
	<- char ispet? if 
		<- char dog? if
			($ <- char .name@ .. " learns sumo, despite being a dog." $) +crtext
			WEAPON_SUMO <- char .weapon_default!
		then
	then;	
	<- char .weapon_default@ WEAPON_SUMO = if
		($ <- char .name@ .. " learns superior sumo techniques!" $) +crtext
		WEAPON_SUPERSUMO <- char .weapon_default!
	else WEAPON_SUMO <- char .weapon_default! then
;

cyoa-blank: common4-vhs-sumo
	"Sumo Wrestling was in the top 5 sports in America before the zombocalypse. This lead to a lot of sumo movies, sumo TV dramas, sumo cartoons, so on and so forth." text!
	($ Thegroup .. " picks up some sumo moves by watching the film!" $) +crtext
	' sumo-vhs foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-shoot
	($ "The videotape is a movie adaptation of the arcade game " .. "DEATHSPLOSIONAL!" fbold .. " It's better than no movie at all." $) text!
	
	' .shooting++ foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-fit
	($ Thegroup .. " follows the the fitness instructor on the videotape. Everyone's wearing neon sweatbands." $) text!
	
	' .fitness++ foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-morale
	($ "The comedy film turns out to be terrible by most standards, but " .. thegroup .. " is completely absorbed by it. It's very rare to see a movie anymore." $) text!	morale3+all
	"The TV shorts out during the middle of the second viewing, and never comes back on." +crtext
;

cyoa: common4-vhs-rare
	"Video Cassette Player" cyoa-title!
	($ Thegroup .. " finds someone's abandoned hideout in a boarded up house.
	There's not much left in here, except a small battery powered TV hooked up to an old VHS player.
	There's only one movie in the videotape player:" $) text!
		choice( "Action Movie" )choice:  common4-vhs-shoot
		choice( "Fitness Tape" )choice: common4-vhs-fit
		choice( "Comedy Film" )choice: common4-vhs-morale
		choice( "Sumo Movie" )choice: common4-vhs-sumo
;

// Spider attack based on a true story where I ate a spider

cyoa-choice: common4-spider-run
	($ Thegroup .. " runs out of that house!
	The spider continues slowly walking on the walls in a creepy, disgusting manner." $) text!
	morale--all badfx
;

cyoa-choice: common4-spider-smoosh
	cchar .trait@ "BERSERK!" $= if
		($ cname .. " screams " .. "BERSERKEERRR!" fbold .. " and then " .. "OBLITERATES" fbold .. " the spider. Only a wisp of smoke remains of it." $) text!
		morale2+ goodfx
	else
		($ cname .. " smooshes the spider. There's a particularly gross crunching noise." $) text!
		cattitude+ if morale-- else morale2- then badfx
	then
		($ Thegroup .. " searches the house and finds some food!" $) +crtext 2 5 rnd trunk-food+
;

cyoa-bridge: common4-spider-smoosh-pick
	solohuman if
		choosehuman common4-spider-smoosh
	then;
	($ "Who should have to smoosh the spider?" $) cyoa-text!
	' common4-spider-smoosh cyoa-human-choice
;

// Gourmand logic below, probably move it somewhere else if I use it more later
: isgourmand? .trait@ "Gourmand" $= ;
: numgourmandfilt args( chara ) <- chara isgourmand? ;
: numgourmands team-stack ' numgourmandfilt stack-filter nip ;

: gourmandfilt ' isgourmand? stack-filter drop ;
: cyoa-gourmand-choice team-stack gourmandfilt cyoa-char-stack-choice ;

cyoa-choice: common4-spider-eat
	($ cname .. " looks around to make sure no one is watching, then quickly eats the spider.
	It's crunchy." $) text! goodfx
	morale++
	cchar isgourmand? if
		($ "I was hungry..." pbold .. "
		-- " .. cname $) +crtext
	then
;

cyoa-bridge: common4-spider-eat-pick
	solopet numgourmands 1 < and if
		choosepet common4-spider-eat
	then;
	($ "Who should eat the spider?" $) cyoa-text!
	' common4-spider-eat cyoa-pet-choice
	' common4-spider-eat cyoa-gourmand-choice
;

cyoa: common4-spider
	"Giant Spider Attack" cyoa-title! someicon
	($ Thegroup .. " tries to rest in a house but is ambushed by a " .. "giant spider!" fbold $) text!
	($ "Well maybe it's a normal spider but it's gross looking. The spider is rapidly draining " .. thegroup .. "'s morale!" $) +crtext
		choice( "Run in disgust" )choice: common4-spider-run
		choice( "Smoosh spider" )choice: common4-spider-smoosh-pick
		numpets 0 > numgourmands 0 > or if choice( "Eat Spider" )choice: common4-spider-eat-pick then
		// choosepet cchar charchoice( ": Eat Spider"
;

// Strange Vending Machine

0 value common4-vending-num

cyoa-choice: common4-vending-go
	"It takes forever to feed in the dollar bills. The vending machine keeps spitting them out." text!
	cattitude+ if morale-- attitudeREV else morale2- then goodfx
	common4-vending-num case 
	1 of 
		"A fully loaded shotgun pops out of the vending machine!" +crtext
		1 WEAPON_SHOTGUN trunk.weapon+ 6 trunk-shotgun-shell+
	endof
	2 of 
		"A loaded cowboy rifle pops out of the vending machine!" +crtext
		1 WEAPON_RIFLE trunk.weapon+ 10 trunk-rifle-shell+
	endof
	3 of 
		"A loaded Dolt revolver pops out of the vending machine!" +crtext
		1 WEAPON_DOLT trunk.weapon+ 6 trunk-bullet+
	endof
	4 of 
		"A bag of chips drops out! Not the usual vending machine size, it's the enormous family size." +crtext 4 trunk-food+
	endof
	5 of 
		"A jerrycan of gasoline plops out of the vending machine!" +crtext 30 trunk-gas+
	endof
	6 of 
		"A large box of medicated bandages pops out of the vending machine!" +crtext 3 trunk-medical+
	endof 
	7 of 
		($ "The vending machine's shape changes! It transforms into an electric car!
			" .. Thegroup .. " gets in." $) +crtext CAR_HYBRID2 get-car 30 trunk-gas+
	endof
	8 of 
		"A stick drops out of the vending machine! I'm not sure what you were expecting, it's a stick." +crtext
		1 WEAPON_TREEBRANCH trunk.weapon+
	endof
	9 of 
		($ "A hat pops out of the vending machine!
		" .. cname .. " puts it on." $) +crtext
		1 27 rnd cchar .hattype! morale++
	endof 
	endcase
;

cyoa-bridge: common4-vending-pick
	solohuman if
		choosehuman cicon
		($ cname .. " is going to have to feed the torn and wrinkled bills into the vending machine. This will be annoying." $) text!
		cchar charchoice( ": Feed bills in" )choice: common4-vending-go
		choice( "Leave it alone" )choice
	then;
	($ "Who should feed the mostly-rotted dollar bills into the machine?" $) cyoa-text!
	"WARNING: This will be a very annoying process" rbold +crtext
	' common4-vending-go cyoa-human-choice
	choice( "Leave it alone after all" )choice
;

: common4-vending-1 1 to common4-vending-num common4-vending-pick ;
: common4-vending-2 2 to common4-vending-num common4-vending-pick ;
: common4-vending-3 3 to common4-vending-num common4-vending-pick ;
: common4-vending-4 4 to common4-vending-num common4-vending-pick ;
: common4-vending-5 5 to common4-vending-num common4-vending-pick ;
: common4-vending-6 6 to common4-vending-num common4-vending-pick ;
: common4-vending-7 7 to common4-vending-num common4-vending-pick ;
: common4-vending-8 8 to common4-vending-num common4-vending-pick ;
: common4-vending-9 9 to common4-vending-num common4-vending-pick ;

cyoa: common4-vending
	"Strange Vending Machine" cyoa-title! someicon
	($ Thegroup .. " runs into a very out of place vending machine. It looks like someone made it from chunks of scrap iron. Its lights are still on.
	Everything in the machine costs $20, and " .. thegroup .. " has about that much in wrinkled and frayed bills." $) text!
		choice( "Leave it alone" )choice
		
		33 %chance if
			choice( "SHOTGUN FUN PACK" )choice: common4-vending-1
		else
			50 %chance if
				choice( "ROOTEM TOOTEM RIFLE" )choice: common4-vending-2
			else
				choice( "REAL REVOLVER SET" )choice: common4-vending-3
			then
		then
		
		33 %chance if
			choice( "BAG OF CHIPS" )choice: common4-vending-4
		else
			50 %chance if
				choice( "GASOLINE CAN" )choice: common4-vending-5
			else
				choice( "BOX O' BANDAGE" )choice: common4-vending-6
			then
		then
		
		10 %chance if
			choice( "A BRAND NEW CAR!" )choice: common4-vending-7
		else
			50 %chance if
				choice( "A STICK" )choice: common4-vending-8
			else
				choice( "A NEW HAT" )choice: common4-vending-9
			then
		then		
;

} }

// Blank
(*
cyoa-choice: common4--

;

cyoa: common4-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)