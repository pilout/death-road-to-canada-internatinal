
uses events/car/walkdecks/carfind1.df

cyoa{ road{ 

: usedlot-carget ($ Thegroup .. " consigue un coche usado nuevo!" $) cyoa-text! goodfx ;
cyoa-choice: usedlot-SUV image-suv usedlot-carget carinfo-SUV CAR_SUV get-car ;
cyoa-choice: usedlot-muscle image-sports-car usedlot-carget carinfo-muscle CAR_MUSCLE2 get-car ;
cyoa-choice: usedlot-hybrid image-hybrid-battery usedlot-carget carinfo-hybrid CAR_HYBRID2 get-car ;

cyoa-choice: usedlot-van image-white-van usedlot-carget carinfo-van CAR_VAN get-car ;
cyoa-choice: usedlot-grandpa image-car-lot usedlot-carget carinfo-grandpa CAR_GRANDPA get-car ;
cyoa-choice: usedlot-little image-car-lot usedlot-carget carinfo-little CAR_BUG get-car ;

cyoa: car1-usedlot
	"Precios tan bajos que son gratis" cyoa-title!
	($ Thegroup .. " ' encuentra una parrilla de coches usados que parece inmune al tiempo, o al menos no tocada por ladrones.	" .. gThey .. " 'encontrar" .. g1s .. " ' posee una reserva de gasolina, y unas pocas coches funcionando!" $) cyoa-text!
		50 80 rnd trunk-gas+ image-car-lot
		// Choices in events/car/walkdecks/carfind1.df
		70 %chance if 		
			choice( "Cambiar a SUV" )choice: usedlot-SUV
		else
			choice( "Cambiar a Muscle Car" )choice: usedlot-muscle
		then
		70 %chance if 	
			choice( "Cambiar a Coche de bolsillo" )choice: usedlot-little
		else
			choice( "Cambiar a Coche clasico" )choice: usedlot-grandpa
		then
		30 %chance if 
			choice( "Cambiar a Coche hibrido electrico" )choice: usedlot-hybrid
		else
			choice( "Cambiar a Camioneta grande" )choice: usedlot-van
		then
		choice( "Mantener el coche, conducir lejos" )choice
;	

: mechanic-armor?
	mechanic-silentrepair
	mechanic-diff 1 > if
		3 car-armor+ ($ "SUPER BLOQUEADO!" fbold $)
	else
		mechanic-diff -1 > if
			2 car-armor+ ($ "BLOQUEADO!" rbold $)
		else
			1 car-armor+ ($ "ARMADURA sort of ARRIBA!" rbold $)
		then
	then
	+cr +text
;
: mech-diff-1-morale mechanic-diff -1 > if morale2+all else morale++all then goodfx ;
cyoa-choice: car1-garage-armor 
	($ cname .. " 'Usa el metal que hay por ahi para crear una armadura improvisada en el coche. Tarda un par de dias." $) cyoa-text!
	mechanic-diff 1 > if
		($ cHe .. " 'realiza un trabajo increible! El coche es practicamente un tanque ahora!" $)
	else
		mechanic-diff -1 > if
		($ cHe .. " 'funciona eficientemente. El coche luce imponente!" $)
		else
		"Parece que el blindaje del coche esta a punto de desprenderse en cualquier momento."
		then
	then 
	+cr +text mechanic-armor? team-count -4 * trunk-food+ mechanicREV mechanic++ 2 calendar-day+
	mech-diff-1-morale heal-event sleep
;

: nocar-garage
	30 50 rnd trunk-gas+ goodfx
	"No hay mucho que hacer aqui sin un coche!" +crtext
;

cyoa: car1-garage
	image-car-garage
	"Garaje Blindado" cyoa-title!
	($ Thegroup .. " 'encuentra un garaje profesional de coches. La senal dice 'CAR PROZ'." .. gThey .. " 'encontrar" .. g1s .. " 'un poco de gasolina y muchos metales viejos." $) cyoa-text!
		vehicle.which@ 0 = if nocar-garage then;
		 " 'Solo necesito tiempo para trabajar en ello." +text
		30 50 rnd trunk-gas+
		// garage-choices are in carlogic.df, and do the standard ones
		garage-choices
		// Every garage can have a different special choice
		trunk-food team-count 4 * 1 - > if
			choice( "Reparar y blindar el coche durante 2 dias" )choice: car1-garage-armor 
		then
;	

: mechanic-megatune?
	mechanic-silentrepair
	mechanic-diff 1 > if
		10 car-megatune+ ($ "SUPER AJUSTE EXTREMO!" fbold $)
		car-mpg 0.6 * to car-mpg
	else
		mechanic-diff -1 > if
			6 car-megatune+ ($ "SUPER AJUSTE!" rbold $)
			car-mpg 0.8 * to car-mpg
		else
			2 car-megatune+ ($ "AJUSTADO?" rbold $)
		then
	then
	+cr +text
;

cyoa-choice: car1-garage-megatune 
	($ cname .. " 'pasa unos dias en el garaje. " .. cHe .. " 'se esfuerza por mejorar el coche hasta sus limites." $) cyoa-text!
	mechanic-diff 1 > if
		($ cHe .. " 'hace cosas increibles! El coche funciona MEJOR que nuevo!" $)
	else
		mechanic-diff -1 > if
		($ cHe .. " 'funciona eficientemente. La vida del coche se extiende, con mejores kilometros por galon!" $)
		else
		"Es dificil decir si el coche ha cambiado en absoluto. Tal vez esta mejor?"
		then
	then 
	+cr +text mechanic-megatune? team-count -6 * trunk-food+ mechanicREV mechanic++ 3 calendar-day+
	mech-diff-1-morale heal-event sleep
;

cyoa: car1-tunegarage
	image-car-garage
	"Garaje Maestro TUNERZ" cyoa-title!
	($ Thegroup .. " 'encontro un garaje de automoviles aislado. La senal dice 'MASTER TUNERZ'.	" .. gThey .. " 'encontrar" .. g1s .. " 'tiene algunas gasolina y muchos piezas de repuesto en buenas condiciones." $) cyoa-text!
		vehicle.which@ 0 = if nocar-garage then;
		30 50 rnd trunk-gas+
		garage-choices
		trunk-food team-count 6 * 1 - > if
			choice( "Repara y ajusta el coche durante 3 dias" )choice: car1-garage-megatune
		then
;	

: mechanic-megaspeed?
	mechanic-silentrepair
		mechanic-diff -1 > if
			1 ' car-speed +! ($ "AUMENTO DE SPEED!" rbold $) +crtext
		then
;

cyoa-choice: car1-garage-megaspeed
	($ cname .. " 'pasa unos dias en el garaje. " .. cHe .. " 'se esfuerza por entender las extranas piezas." $) cyoa-text!
		mechanic-diff -1 > if
		($ cHe .. " 'las instala. Era un BOOSTER ROQUETA!" $)
		else
		"Las partes quedan un misterio y se quedan atras. El coche recibe algunas reparaciones."
		then
	+cr +text mechanic-megaspeed? team-count -6 * trunk-food+ mechanicREV mechanic++ 3 calendar-day+
	mechanic-diff 1 > if morale3+all goodfx else mech-diff-1-morale then heal-event sleep
;

cyoa: car1-speedgarage
	image-car-garage
	"Garaje Brillante" cyoa-title!
	($ Thegroup .. " 0x3A3E3E5E," .. gThey .. " 'encontrar" .. g1s .. "  algo de gasolina y algunas piezas misteriosas de coche." $) cyoa-text!
		vehicle.which@ 0 = if nocar-garage then;
		30 50 rnd trunk-gas+
		garage-choices
		trunk-food team-count 6 * 1 - > if
			choice( "3 days of repair and attempt mod" )choice: car1-garage-megaspeed
		then
;	

cyoa-choice: car1-floodroad-plow
	($ Thegroup .. " decide tomar una oportunidad y arar el coche a traves de la carretera inundada." $) cyoa-text! car-plowthrough	
;
cyoa-choice: car1-floodroad-jump
	($ Thegroup .. " 've una rampa improvisada y tiene la idea espectacularmente peligrosa de intentar saltar la parte inundada de la carretera." $) cyoa-text! car-jump
;
cyoa: car1-floodroad
	image-water-driving
	"Camino Inundado" cyoa-title!
	vehicle.which@ 0 = if 
		($ "El camino adelante esta inundado. Sin coche,' " .. thegroup .. " tiene que dar un largo rodeo a pie." $) text! 1 calendar-day+ +cr fullcamp
	then;
	($ "La carretera adelante ha sido inundada, pero parece que el coche podria tal vez todavia pasar por ahi." .. Thegroup .. " decide a:" $) cyoa-text!
		// carlogic.df again
		choice( "Tomar un desvio" )choice: car-detour
		choice( "Arar" )choice: car1-floodroad-plow
		angriest choosechar chumany? if
			cchar charchoice( ": SALTARLOS" )choice: car1-floodroad-jump
		then
;	

cyoa-choice: car1-fallentree-plow
	($ Thegroup .. " 'piensa que es solo un arbol, y el coche esta hecho de metal. Que tan dificil podria ser atropellarlo?" $) cyoa-text! car-plowthrough	
;
cyoa-choice: car1-fallentree-shoot
	($ Thegroup .. " 'decide sacar las armas y disparar al arbol muerto." $) cyoa-text!
	// car-shoot-check if
	// Retooling this a bit later
		"El plan de alguna manera funciona! El arbol es volado en pedazos." +cr +text goodfx
	(* else
		($ "Eso no funciono en absoluto. " .. Thegroup .. " ' intenta chocar el coche contra el arbol con hojas de plata." $) +cr +text car-plowthrough
	then *)
	car-shoot
;
: car1-fallentree-pop dup .morale-- 50 %chance if dup .strength-- else dup team-attacked then ;
cyoa-choice: car1-fallentree-lift
	choosehuman
	($ Thegroup .. " ' decide intentar recoger el arbol." $) cyoa-text!
	strength-total 10 > if
		($ Thegroup .. " ' saca el arbol de la carretera a traves del fuerte poder!" $) +cr +text goodfx
	else
		strength-total 5 > if
			($ Thegroup .. " moves the tree out of the road through extreme effort. 
			" .. cname .. " ' tiene algo que se desplome dentro de su cuerpo!" $) +cr +text 
				cchar team-attacked morale-- strength-- badfx
		else
			($ Thegroup .. " ' no puede mover el arbol en absoluto, pero consigue herirse a si mismo bastante. " .. ghim .. "a si mismo. " .. gThey .. " ' toman la decision" .. g1s .. " ' intentan chocar el arbol con el coche."  $) +cr +text
			car-plowthrough ' car1-fallentree-pop foreach-team-char
		then
	then
;
cyoa: car1-fallentree
	image-tree-in-road
	"Arbol en la Carretera" cyoa-title!
	"La carretera adelante tiene un gran arbol caido sobre ella. Parece que alguien lo corto para bloquearla. Tomaria a un grupo de personas fuertes para moverlo seguramente!" text!
	vehicle.which@ 0 = if 
		($ "Sin coche, " .. thegroup .. " 'solo se arriesga a cruzar el arbol caido." $) +crtext morale++all goodfx
	then;
	($ Thegroup .. " decide a:" $) +crtext
		// carlogic.df again
		choice( "Tomar un desvio" )choice: car-detour
		choice( "Arar" )choice: car1-fallentree-plow
		choice( "Levanta el arbol" )choice: car1-fallentree-lift
		// Add an event for having a chainsaw?
		car-shoot-check if
			choice( "DISPARA A ELLA" )choice: car1-fallentree-shoot
		then
;	

cyoa-choice: car1-dashcode-mech
	($ cname .. " 'verifica el coche y encuentra el problema. Era solo una tapa de gasolina suelta. Facil de arreglar!" $) cyoa-text!
		cmechanic- if mechanic++ then
		mechanicREV goodfx 2 4 rnd car-tune+
;
cyoa-choice: car1-dashcode-punch
	cpet? if
		($ cname .. " 'muerde a saco el coche, de verdad." $)
	else
		($ cname .. " ' aplasta el coche con " .. chis .. " 'sonido." $)
	then cyoa-text!	
	"Alivia un poco de estres y la luz de error se apaga!" +crtext
		cchar .strength 4 < if strength++ then
		morale++ strengthREV goodfx -1 car-chassis+
;
cyoa-choice: car1-dashcode-kick
	($ cname .. " 'da un punetazo al coche desde fuera y se desahoga con " .. chis .. " 'los pies. Que entrenamiento! La luz de error sigue encendida, sin embargo." $) cyoa-text!
		cchar .fitness 4 < if fitness++ then
		morale++ fitnessREV goodfx -1 car-chassis+
;

: car1-dashcode-dtext cpet? if "muerde" else "golpea" then ;

cyoa: car1-dashcode
	vehicle.which@ 0 = if 
		image-zombie-hoard-1
		"Caminata a Canada" cyoa-title!
		($ Thegroup .. " 'le falta la proteccion de un coche a medida que se acercan a Canada. " .. gTheyare .. " 'atacado por una gran horda al aire libre. No hay lugar donde esconderse y no hay tiempo para luchar." $) text! ouch--all bitefx
	then;
	image-check-engine
	"SERVICIO DE MOTOR PRONTO" cyoa-title!
	($ "El coche funciona bien, pero un coÌdigo de error aparece en el tablero. " .. Thegroup .. " 'no tiene ni idea de que esta mal, asi que '; " .. gthey .. " 'tira" .. g1s .. "  sobre para ver que pasa." $) cyoa-text!
		choice( "Ignorar" )choice
		mechanicest charchoice( " lo inspecciona cuidadosamente" )choice: car1-dashcode-mech
		strongest choosechar cchar charchoice( " " .. car1-dashcode-dtext .. " es muy dificil" )choice: car1-dashcode-punch
		fittest charchoice( " 'lanza mucho" )choice: car1-dashcode-kick
;

} }
