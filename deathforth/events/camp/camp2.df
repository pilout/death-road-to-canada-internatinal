
cyoa{ mission{

cyoa: camp2-fightdone
	($ "La pire de la siege de zombies est terminee " .. Thegroup .. " il nettoie les derniers zombies restants a lexterieur, puis rentre pour ecraser les derniers et permettre a " .. gthey .. " de mettre en place le camp." $) cyoa-text! morale++all +cr road{ fullcamp }
;

} }

cyoa{ road{

// Minigolf, no choices

cyoa: camp2-minigolf
	image-golf-course
	"Mini Golf!" cyoa-title!
	($ Thegroup .. " sinstalle au camping le plus sur quils puissent trouver : un parcours de mini golf abandonne. " $) cyoa-text! basic-tone
	+cr fullcamp
	($ "Le matin " .. gthey .. "  trouver" .. g1s .. " une balle de golf et un putter en cherchant la zone. " .. Thegroup .. " decide de jouer une petite partie!" $) +crtext
	unfittest choosechar cfitness- if
		cpet? if
			($ cname .. "  court partout avec excitation!" $) +crtext morale++
		else
			($ cname .. "  fait un peu dexercice!" $) +crtext fitness++
		then	
	then
	most-ahole if
		choosechar ($ cname .. "  se lasse du jeu et perd ensuite la balle expres." $) +crtext morale--all
	then;
	strongest-pass if
		cpet? if
		 	($ cname .. "  mord accidentellement la balle." $) +crtext morale++ 1 WEAPON_GOLF trunk.weapon+
		then;
		strongest choosechar 50 %chance if
			($ cname .. "  detruit accidentellement le putter." $) +crtext morale++
		else
			($ cname .. "  frappe accidentellement la balle si loin quelle se perd." $) +crtext fitness++ 1 WEAPON_GOLF trunk.weapon+
		then
	then;
	morale++all 1 WEAPON_GOLF trunk.weapon+
;

// Driving range with different choices

// previously they find a less dangerous and less fun place to camp - fullcamp
cyoa-choice: camp2-fore-flee
	($ Thegroup .. "  decide de fuir le terrain de golf " .. gThey .. "  finissent par ne pas trouver un autre endroit pour dormir, et conduisent pendant la majeure partie de la nuit " .. gThey .. "  sont tres fatigues le lendemain." $) cyoa-text! +cr tiredcamp
;
cyoa-choice: camp2-fore-fore
	solo if
		soloer choosechar
		cpet? if
			($ cname .. " ' tente de jouer au golf, mais cest difficile a faire en tant que " .. cdogcat .. ". " .. cHe .. " maybe could have done it with a caddy.
			" .. cname .. "  est completement depasse." $) cyoa-text! morale3- ouch-- sting-- bitefx 
		else
			($ cname .. " ' frappe un tas de balles de golf dans des tetes de zombies. Cest un assez bon exercice, mais bientot " .. che .. " is overwhelmed by them.
			" .. cHe .. "  sechappe, mais passe la nuit cache dans un placard " .. cHe .. "  est epuise le matin." $) cyoa-text! fitness++ fitnessREV morale-- sting-- bitefx  
		then someone choosechar alive? if +cr tiredcamp then
	then;
	($ Thegroup .. "  prend un tas de clubs et recupere quelques seaux de balles de golf. Ils commencent a tirer des coups sur le parcours alors que les zombies se rapprochent." $) cyoa-text! 
	fitness-total 7 > if
		"Ils envoient balle de golf apres balle de golf dans la horde qui arrive." +crtext
		strength-total 7 > if
			"Lequipe frappe les zombies de lautre cote du parcours. Ils ne sapprochent meme jamais !" +crtext goodfx morale++all
		else
			"'Les zombies se font bombarder de balles de golf, mais ils ne sont pas assez durement touches pour etre ralentis. L'equipe finit par les combattre normalement." +crtext someone .sting-- someone .sting-- bitefx
		then
		unfittest .fitness++ ' .fitnessREV foreach-team-char 
		' .strengthREV foreach-team-char +cr fullcamp
	else
		($ "'C'est un travail difficile, et l'equipe s'epuise surtout " .. gThey .. "  sont submerges par les zombies." $) +crtext ouch--all ' .fitnessREV foreach-team-char +cr choosehuman alive? if fullcamp then bitefx
	then
	1 WEAPON_GOLF trunk.weapon+
;
cyoa-choice: camp2-fore-mech
	($ cname .. " ' saute dans un ramasse balles de golf et commence a ecraser les zombies. C'est etonnamment efficace. Au moment ou le ramasse balles tombe en panne, la plupart des zombies sont elimines." $) cyoa-text! morale++all mechanicREV mechanic++ +cr fullcamp
;
cyoa-bridge: camp2-fore-fight
	($ "'Les zombies commencent a frapper la porte du magasin de golf. Il n'y a pas beaucoup de temps pour se preparer avant que la porte ne se brise !" $) cyoa-text!
		2.4 20 22 rnd 1 siegecast // Formerly 1.3 then 1.7 diff
		"events/siege/region/golfcamp.df" $load-region
		choice( "Essayez de survivre" )choice: premission
;

cyoa: camp2-fore
	image-golf-course
	"Dernier combat du practice de golf" cyoa-title! 
	choosehuman
	($ Thegroup .. "  campe dans un magasin de golf pres dun parcours de golf " .. cname-He .. "  regarde par la fenetre et voit un grand groupe de zombies approcher du practice de golf !" $) cyoa-text! basic-tone 3 WEAPON_GOLF trunk.weapon+
	mechanicest-pass if
		mechanicest charchoice( "  branche un ramasse balles de golf" )choice: camp2-fore-mech
	then
		choice( "Tirez des balles de golf sur les zombies" )choice: camp2-fore-fore
		choice( "Preparez vous au siege!" )choice: camp2-fore-fight
		choice( "Conduisez sans dormir" )choice: camp2-fore-flee
;

// No sleep campground

cyoa-choice: camp2-smell-morale
	someicon
	($ Thegroup .. "  endure le " .. "ZONE DODEUR" rainbold .. "  toute la nuit, jusqua levanouissement. " .. gTheyare .. "  pas content le matin, mais " .. gtheywere .. "  au moins capable de dormir." $) cyoa-text! morale--all +cr fullcamp
;

cyoa-choice: camp2-smell-no
	image-camp-1
	($ Thegroup .. "  sort du " .. "ZONE DODEUR" rainbold .. ". " .. gThey .. "  obtenir" .. g1s .. "  pas de sommeil, mais ressentez" .. g1s .. "  comme si partir etait le bon choix." $) cyoa-text! +cr tiredcamp
;

cyoa: camp2-smell
	image-camp-1
	"Camping malodorant" cyoa-title! 
	($ "Le camping qui " .. thegroup .. "  sinstalle sent terrible! La source de lodeur est un mystere complet." $) text!
		choice( "Supporter le parfum" )choice: camp2-smell-morale
		choice( "Quitte sans sommeil" )choice: camp2-smell-no
;

// Cut right before release due there already being a lot of tired things, plus this one was unavoidable
cyoa: camp2-creepy
	image-camp-1
	"Camping effrayant" cyoa-title! 
	($ "Le camping qui " .. thegroup .. " 'tombes sur cela est tres inquietant. Il est difficile de savoir pourquoi c'est si derangeant." .. gThey .. "  obtenir" .. g1s .. " sans sommeil, et ressent" .. g1s .. " en reste meme apres lavoir quitte." $) cyoa-text! +cr tiredcamp
;

// Night watch for bandits, or rather thieves

cyoa-choice: camp2-bwatch-drive
	($ Thegroup .. " refuse de camper dans une zone dangereuse et passe une nuit tendue et sans sommeil en conduisant a travers le noir." $) cyoa-text! morale--all +cr tiredcamp
	50 %chance if "La voiture devie de la route et heurte un arbre." +crtext  -2 car-chassis+ then
;
cyoa-choice: camp2-bwatch-pick
	($ cname .. " garde la veille pour la nuit" $) cyoa-text!
	cloyalty- if
		($ cHe .. " ignore la tache et tombe a leveil presque instantanement. Le matin, beaucoup dapprovisionnements manquent." $) +crtext sleep bandit-supplies2
	else
		($ cHe .. " arrive a rester eveille, mais se sent mal au matin." $) +crtext
		sleep-except morale--
	then loyaltyREV
	+cr chowtime
;

cyoa-bridge: camp2-bwatch-pick4
	someicon
	"Qui devrait rester veiller la nuit?" cyoa-text!
	' camp2-bwatch-pick cyoa-team-choice
;

cyoa: camp2-bwatch
	image-bandit-watch
	"Bandit Watch" cyoa-title! 
	($ "Qui devrait rester eveille pour surveiller?" $) cyoa-text! basic-tone
		team-count 3 > if
			' camp2-bwatch-pick 2 #cyoa-team-choice
			choice( "Choisissez quelquun dautre" )choice: camp2-bwatch-pick4
		else
			' camp2-bwatch-pick 3 #cyoa-team-choice
		then
		choice( "Continuer a conduire" )choice: camp2-bwatch-drive
;

// Night watch for zombies

cyoa-choice: camp2-zwatch-awake
	($ cname .. " decide de se coucher, pariant que les zombies ne trouveront pas " .. chim .. "." $) cyoa-text!
	50 %chance if
		($ cHe .. " se reveille au milieu de se faire manger par un petit groupe de zombies. Sans personne pour laider. " .. chim .. ", c'est difficile de se liberer." $) +crtext despairbite
	else
		($ cHe .. " " $) +crtext
	then
	someone choosechar alive? if +cr fullcamp then
;
cyoa-choice: camp2-zwatch-shift
	someone choosechar
	($ Thegroup .. " prend des tours pour dormir pendant la nuit." $) text!
	team-count 4 < if 
		($ cname .. " is very tired in the evening " .. theothers .. " n'ai pas bien dormi non plus." $) +crtext sleep-except 5050nosleep morale--all +cr chowtime
	else
		($ "Avec un groupe complet, il y a assez de gens pour dormir en paix!" $) +crtext +cr fullcamp
	then
;
cyoa-choice: camp2-zwatch-pick
	($ cname .. " surveille les zombies errants." $) cyoa-text!
	cloyalty- if
		cchar statmod{ .soundsleep? } if
			($ cHe .. "  cesse de preter attention et segare au milieu de sa garde. " .. gHe .. "  est pris en embuscade par des zombies, mais parvient a les combattre." $) +crtext
			cchar ' .sting-- foreach-team-char-except
		else
			($ cHe .. "  sendort presque instantanement. " .. gHe .. "  se reveille au milieu dun repas." $) +crtext ouch--all
		then sleep
	else
		($ cHe .. "  parvient a rester eveille, mais se sent groggy le matin." $)
		+crtext sleep-except morale--
	then loyaltyREV
	someone choosechar alive? if +cr chowtime then
;

cyoa-bridge: camp2-zwatch-pick4
	someicon
	"Qui devrait rester veiller la nuit?" cyoa-text!
	' camp2-zwatch-pick cyoa-team-choice
;

cyoa: camp2-zwatch
	image-zombie-watch
	"Veille de zombie" cyoa-title! 
	($ "'Il se fait tard et il n'y a pas d'endroit ideal pour camper. Des zombies solitaires clopinent au loin, et l'un d'eux pourrait se faufiler sur " .. thegroup .. " tant que " .. gthey .. "  dormir" .. g1s .. ". Qui devrait rester eveille pour surveiller ?" $) cyoa-text! basic-tone
		team-count 3 > if
			' camp2-zwatch-pick 2 #cyoa-team-choice
			choice( "Choisissez quelquun dautre" )choice: camp2-zwatch-pick4
		else
			' camp2-zwatch-pick 3 #cyoa-team-choice
		then		
		solo if
			choice( "Dormir quand meme" )choice: camp2-zwatch-awake
		else
			choice( "Prendre des quarts" )choice: camp2-zwatch-shift
		then
;

// Berry picking

cyoa-choice: camp2-berry-go
	($ Thegroup .. "  cueille des baies toute la nuit et les met dans tous les recipients a portee de main." $) cyoa-text! numhums 3 * 2 + trunk-food+
	+cr tiredcamp
;

cyoa-choice: camp2-berry-no
	($ Thegroup .. " ' n'a vraiment pas envie de cueillir des baies pendant 8 heures." $) cyoa-text!
	+cr fullcamp
;

cyoa: camp2-berry
	image-camp-2
	"Francais" cyoa-title!
	($ Thegroup .. " ' installe le campement pour la nuit apres s'etre assure qu'il n'y a aucun danger ici." $) cyoa-text! 
		($ gThey .. "  decouvrir" .. g1s .. " ' buissons de baies comestibles tout autour du camping !				Les baies ne seraient pas une grande quantite de nourriture pour le travail et la nuit blanche, mais chaque petit geste pourrait aider." $) +crtext
		choice( "Cueillir des baies toute la nuit" )choice: camp2-berry-go
		choice( "Aller dormir" )choice: camp2-berry-no
;


// Scary Stories

: camp2-scary-noun 
	0 stack
		"GOBLINS!!!!" shove
		"SQUELETTES !!" shove
		"MAUVAISES ODEURS !" shove
		"ENORMES PETOURS !" shove
		"DIARRHEA!!!" shove
		"MINUSCULE FANTOMES !" shove
		"A TURTLE!!!" shove
		"MECHANTS CHATS !!" shove
		"REPARATION DE VOITURE !" shove
		"CHIENS ABOYEURS !" shove
		"GOBELINS MECHANTS !" shove
		"SQUELETTES DANSANTS !" shove
		"ALLERGIES AUX ARACHIDES!" shove
		"LAUGHING GOBLINS!!!" shove
		"GOBELINS!!" shove
		"LES GOBELINS AGRESSIFS!" shove
	shuffles pops nip
;

cyoa-choice: camp2-scary-go
	($ Thegroup .. " decide de raconter des histoires effrayantes!" $) text!
	solo if
		($ "Mais il savere que " .. soloname .. " " .. "TELLING THE SCARY STORIES TO GHOSTS!
		WHOOOOOAAAAAAA!" fbold $) +crtext morale3+all someicon
	else
		choosehuman cicon
		cpet? if
			($ cname .. " la plupart du temps " .. cbarkmeow .. "il y a beaucoup mais cest dans le sens de " .. "CHEMIN EFFRAYANT!" fbold $) +crtext
		else
			($ cname .. " fait " .. chis .. " histoire effrayante a propos de " .. camp2-scary-noun fbold $) +crtext
		then morale2+all	
	then
	sfx{ creepy 30 fx .delay! }
	($ Thegroup .. " ' s'amuse bien, mais ne dort pas assez et est fatigue le lendemain." $) +crtext nosleep
;

cyoa: camp2-scary
	image-camp-1
	"Plus de Creepypastas" cyoa-title!
	($ Thegroup .. "  est assis autour dun feu de camp, se reposant avant une journee brutale demain." $) text!
		+cr fullcamp
		choice( "Raconter des histoires effrayantes" )choice: camp2-scary-go
		choice( "Va juste dormir" )choice
;

// Bad Weather camping

: camp2-badweather-check args( @ )
	<- @ oblivious? if
		($ <- @ .colourname .. "  pensait que le temps etait agreable !" $) +crtext 
		<- @ choosechar morale++ witsattREV
	then;
	<- @ .cattitude+ if 
		($ <- @ .colourname .. " ' ne s'en souciait pas beaucoup." $) +crtext 
		<- @ .attitudeREV
	then;
	<- @ .morale--
;

cyoa: camp2-badweather
	image-camp-2
	"Camping miserable" cyoa-title!
	($ Thegroup .. " ' n'a pas pu trouver d'abris surs, mais a pu bricoler un camping. Le temps est terrible toute la nuit !" $) text! ' camp2-badweather-check foreach-team-char +cr fullcamp
	0.4 1 frnd weather! -1 1 frnd wind_force!
;


// Woodstove camping

: camp2-wood-fire args( @ )
	<- @ .cattitude- if <- @ .attitudeREV then;
	<- @ .morale++
;

cyoa-choice: camp2-wood-chopped
	($ cname .. "  coupe un tas de bois et allume le poele a bois avant de sevanouir depuisement." .. cHe .. "  sera tres fatigue demain !" $) text!
	strengthREV strength++ cchar tired goodfx
	"Le feu fait du bien !" +crtext
	' camp2-wood-fire foreach-team-char
;

cyoa: camp2-wood-chop
	solohuman if
		choosehuman camp2-wood-chopped
	then;
	"'Qui devrait aller couper le bois ? C'est vraiment un travail difficile !" cyoa-text!
	' camp2-wood-chopped cyoa-human-choice
;

cyoa: camp2-wood
	image-cabin-in-woods
	"Cabane avec poele a bois" cyoa-title!
	($ Thegroup .. "  trouve une cabane dans les bois, sans zombies en vue. Il y a un poele a bois dedans, mais pas de bois !" $) text! +cr fullcamp
		choice( " Sortez et coupez du bois" )choice: camp2-wood-chop
		choice( "Va juste dormir" )choice
;

// Creepy inn woman

: camp2-airbnb-eat args( @ )
	<- @ paranoid? if
		($ <- @ .colourname .. "  refuse de le manger !" $) +crtext <- @ .morale-- 
		<- @ .witsREV <- @ .attitudeREV
	then;
	<- @ .morale2+
;

: camp2-airbnb-breakfast
	($ " Laubergiste sert un petit dejeuner incroyable !" $) +crtext
	' camp2-airbnb-eat foreach-team-char goodfx	sleep
;

cyoa-choice: camp2-airbnb-pay
	($ Thegroup .. " ' paie pour passer une nuit a l'auberge. C'est confortable.	" .. "No one wants zombies creepin while they're sleepin hhhHHHHHHeheh." rbold $) text! -10 trunk-food+
	camp2-airbnb-breakfast
;

cyoa-choice: camp2-airbnb-charm
	($ cname .. "  obtient une meilleure offre pour la nuit.	" .. "You're a sly one hhhhhhhhhhHHHHHHeheh." rbold $) text! -5 trunk-food+
	camp2-airbnb-breakfast
;

: camp2-airbnb-pres
		($ "...hhhhhhhhhhHHHHHHeheh you must be a big shot.
		I'll show you the presidential suite." rbold $)
		+crtext
;

cyoa-choice: camp2-airbnb-coolit
	cpet? if
		($ cname .. "  fait un " .. "hhhHHHHHeh" rbold .. "  bruit." $)
	else
		($ cname .. "  dit a la femme de " .. " DITES LE PAS SPRAYEZ LE." pbold $)
	then text!
	cool-it-7 if
		camp2-airbnb-pres
		"' Elle montre la suite presidentielle ! Elle a un spa !" +crtext
		morale3+all 
		" Elle montre aussi le gymnase executif !" +crtext
		' .fitness++ foreach-team-char ' .strength++ foreach-team-char
		camp2-airbnb-breakfast
		cooled-it goodfx
	else cool-it++
		camp2-airbnb-pres 
		($ " Elle donne des coups de pied a " .. thegroup .. "  dehors " .. gThey .. "  manger" .. g1s .. "  sur la route, mais ne trouve jamais" .. g1s .. "  un endroit sur pour dormir." $) +crtext badfx +cr tiredcamp
	then witsattREV 
;

cyoa-choice: camp2-airbnb-nosleep
	($ Thegroup .. "  sort de la tres vite. " .. gThey .. "  manger" .. g1s .. "  sur la route, mais ne trouve jamais" .. g1s .. "  un endroit sur pour dormir." $) text! +cr tiredcamp
;

cyoa: camp2-airbnb
	image-old-house
	"Bed & Breakfast" cyoa-title!
	($ Thegroup .. "  trouve une auberge, ouverte aux affaires ! La proprietaire se tord les mains: 	" .. "Give me 10 food, I'll watch over you while you sleep hhhHHHHHeh." rbold .. "	Elle est super flippante." $) text!
		trunk-food 9 > if	
			choice( "Paye les 10 nourritures" )choice: camp2-airbnb-pay
		then
		most-grating if	
			charchoice( "': DIS LE NE LE PULVERISE PAS" fcoolit )choice: camp2-airbnb-coolit
		then
		most-charming trunk-food 5 > and if
			charchoice( "  marchande jusqua 5 nourritures" )choice: camp2-airbnb-charm
		then
		choice( "Quitte sans sommeil" )choice: camp2-airbnb-nosleep
;

// Zombie singalong

: camp2-sing-team args( @ )
	<- @ .attitudeREV
	<- @ .cattitude+ if
		<- @ .morale++
	then
	<- @ .cattitude- if
		<- @ .morale--
	then	
;

cyoa-choice: camp2-sing-sing someicon
	($ Thegroup .. "  essaie de garder le moral en chantant. Que le chant soit bon ou non depend surtout de ton attitude." $) cyoa-text! 3 700 tone 2 500 tone
		' camp2-sing-team foreach-team-char
;

cyoa-choice: camp2-sing-plan
	cwits- if
		($ "Il savere que " .. thegroup .. " ' n'est vraiment pas doue pour planifier les choses du tout." $) cyoa-text! morale--all ' .witsREV foreach-team-char badfx
	then;	
	cwits+ witsroll or if
		($ cname .. " ' planifie le lendemain. C'est une bonne utilisation du temps, et aide " .. ghim .. "  se concentre sur le tableau densemble." $) cyoa-text!
			morale++ goodfx
	else
		($ cname .. " ' elabore des plans pour le lendemain. Ce n'est que le matin que " .. ghe .. "  se rend compte que les plans etaient completement fous." $) cyoa-text!
			morale--all badfx
	then witsREV
;

cyoa: camp2-sing
	"Chant de mort sur la route" cyoa-title!
	($ Thegroup .. "  est assis autour dun feu de camp. Trouver ce camping etait difficile, donc " .. gtheyare .. "  de mauvaise humeur." $) cyoa-text! +cr fullcamp
		choice( "Chante une chanson" )choice: camp2-sing-sing
		smartest charchoice( ": Planifier demain" )choice: camp2-sing-plan
;
		
} }


