// this is called after carstats.df
events{ road{

: car-lost-old
	// 0 to car-working
	0 vehicle.which!
	walk-music
	// temporary wear thingy
	car-max-engine to car-engine
	car-update-display
	badfx
	walk-day-pick
	// OLD walk-day-chance
;

: car-boom
	($ "EL CARRO FUE ANiquILADO!		Armas de maleteria perdidas!" rbold $) +cr +text
	car-lost trunk.killer
;

: car-break
	($ "EL CARRO FUE DESTROIDO!" rbold $) +cr +text
	car-lost
;

: car-stolen
	($ "EL CARRO FUE ROBADO!" rbold $) +cr +text
	car-lost
;

: car-abandon
	($ "EL CARRO FUE ABANDONADO!" rbold $) +cr +text
	car-lost
;

: car-fullhealth
	car-max-chassis to car-chassis
;

: car-fulltune
	car-max-engine to car-engine
;

: car-gas+
	trunk-gas+
	trunk-gas 0 < if
		0 LOOT_GAS trunk.loot!
	then
;

: car-chassis+
	' car-chassis +!
	car-chassis car-max-chassis > if
		car-fullhealth
	then;
	car-update-display
	car-chassis 0 <= if
		car-break
	then
;

: car-armor+
	dup ' car-max-chassis +!
	dup car-chassis+
	1 ' car-armor +!
;

: car-tune+
	' car-engine +!
	car-engine car-max-engine > if
		car-fulltune
	then
;

: car-megatune+
	dup ' car-max-engine +!
	dup car-tune+
;

: car-guzzle 10 car-mpg * car-armor + ;
: guzzle-mpg 
	trunk-gas car-guzzle < if
		trunk-gas
	else
		car-guzzle
	then
	"BEBED: " type dup print
	-1 * trunk-gas+ 
;

// Logic for garage-choices

: car-work-randnum
	cchar .mechanical 7 < if 2 else 1.4 then // For over-max mechanical, get a tighter repair variance
;

: car-engine-rnd car-max-engine car-work-randnum / car-max-engine rnd ;
: car-tune-rnd car-engine-rnd car-tune+ ;
: car-tune-rnd-half car-engine-rnd 2 / car-tune+ ;

: car-chassis-rnd car-max-chassis car-work-randnum / car-max-chassis rnd ;
: car-weld-rnd car-chassis-rnd car-chassis+ ;
: car-weld-rnd-half car-chassis-rnd 2 / car-chassis+ ;

: mechanic-diff
	cchar .mechanical car-repair - 
;
: mechanic-silentrepair 
	mechanic-diff -1 > if car-tune-rnd car-weld-rnd else car-tune-rnd-half car-weld-rnd-half then
;

: mechanic-weldup // Combine with tuneup
	mechanic-diff 1 > if
		car-weld-rnd car-tune-rnd goodfx
		($ cHe .. " 'expertamente repara el carro en poco tiempo." $)
	then;
	mechanic-diff -1 > if
		car-weld-rnd car-tune-rnd
		"Las reparaciones toman una buena parte del dia."
	then;
	mechanic-diff -2 > if
		car-weld-rnd-half car-tune-rnd-half
		"Las reparaciones toman una buena parte del dia, y son muy dificiles."
	then;
	($ cHe .. " no tiene ni idea de que " .. che .. "s doing, and wastes a day's time pretending to work on the car." $)
;
: mechanic-day?
	mechanic-diff 2 < if
		fullcamp
	then
;

// Used to repair a car that's fully broken down
: mechanic-repair? cchar .mechanical car-repair ;
: mechanic-repair
	car-engine-rnd to car-engine
	1 ' car-repair +!
;
: car-tunecheck
	mechanic-repair? > if
		($ cname .. " repara facilmente el coche en un instante!" $) +cr +text goodfx
		mechanic-repair mechanicREV
	then; 
	mechanic-repair? = if
		mechanicroll if
			($ cHe .. " 'de alguna manera logra hacer que el coche arranque, pero le lleva todo un dia." $) +cr +text goodfx
			cchar .mechanical@ 6 > if
				($  "La proxima vez que este coche se descomponga, " .. cname .. " absolutamente no podra repararlo." rbold $) +crtext
			else
				($ "La proxima vez que este coche se descomponga, " .. cname .. " no podra repararlo a menos que " rbold .. chis rbold .. " 'La habilidad mecanica es mas alta!" rbold $) +crtext
			then
			mechanic-repair
		else
			($ cHe .. " pierde un dia intentando reparar el coche, pero se niega a arrancar." $) +cr +text car-abandon	
		then
	else
		($ cHe .. " 'No logra reparar el coche despues de perder un dia en el." $) +cr +text car-abandon
	then mechanicREV +cr fullcamp
;

: car-strengthcheck
	strengthroll if
		($ cHe .. "  esta tan sorprendido como cualquiera cuando el motor cobra vida!" $) +cr +text
		mechanic-repair
		car-chassis cchar .strength - 1 < if
			"El coche no llega muy lejos por la carretera antes de que alguna parte que fue golpeada se incendie." +cr +text
		then
		cchar .strength -1 * car-chassis+ strengthREV
	then;
		($ cHe .. "  'apenas logra romper algo en el coche y algunos huesos en' " .. chis .. " mano" $) +cr +text car-abandon morale-- strengthREV strength--
;

// Road block encounter common stuff
	
: detour-roll
	car-mpg 0 = if
		"Curiosamente, este coche no necesita gasolina." +cr +text
		goodfx
	then;
	50 %chance if
		($ gThey .. " 1" $)
		+cr +text
		trunk-gas 70 car-mpg * >= if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			bandit-gas
		then
		+cr fullcamp heal-event
	else
		"Tiene que pasar un poco mas de tiempo y gas, pero no mucho!" +cr +text
		trunk-gas 30 car-mpg * >= if
			-25 -30 rnd car-mpg * car-gas+
		else
			bandit-gas
		then
	then
;

: car-plowthrough
	car-armor 0 > if
		"El coche choca directamente a traves de la obstaculo, aunque algunas piezas de su armadura improvisada caen." +cr +text goodfx
		-1 ' car-armor +!
	else
		-2 -3 rnd car-chassis+ badfx
		car-chassis 0 > if
			"El coche pasa, pero sufre algun dano." +crtext
		else
			"El coche paso, pero tomo demasiado dano. Se desintegro a corta distancia por el camino." +crtext
		then 
	then
;

: car-jump
	car-speed 2 < if
		"El coche lento se vuelve hacia arriba y se encendio en llamas, no logrando superar la obstaculo." +cr +text badfx
		ouch2-all car-break
	then;
	car-speed 2 = if
		"El coche solo pasa, pero hunde como un ancla y se estrella en el suelo. Pedazos del coche volan en varias direcciones." +cr +text badfx
		-2 -3 rnd car-chassis+
		car-chassis 0 > if "Se le da una paliza, pero sigue corriendo!" +crtext then
	then;
	car-speed 3 > if
		"El coche acumula una gran velocidad, choca contra la rampa y comienza a volar por un tiempo debido a los cohetes de propulsion! Esta emocionante!" +cr +text goodfx morale2+all
	then;
	car-speed 2 > if
		"El coche acumula una gran velocidad, choca contra la rampa y vuela por encima de la obstaculo!" +cr +text goodfx
	then;
;
	
// Option for shooting stuff in a frenzy, needs work later

: car-shoot-ammo? trunk-bullet 0 > trunk-rifle-shell 0 > trunk-shotgun-shell 0 > or or ;
: car-shoot-check trunk-bullet 29 > trunk-rifle-shell 20 > trunk-shotgun-shell 10 > and and ;
: car-shoot // improve this later
	trunk-bullet 0 > if -30 trunk-bullet+ then
	trunk-rifle-shell 0 > if -20 trunk-rifle-shell+ then
	trunk-shotgun-shell 0 > if -10 trunk-shotgun-shell+ then
	trunk-bullet 0 < if
		0 LOOT_BULLET trunk.loot!
	then
	trunk-shotgun-shell 0 < if
		0 LOOT_SHOTGUN_SHELL trunk.loot!
	then
	trunk-rifle-shell 0 < if
		0 LOOT_RIFLE_SHELL trunk.loot!
	then
;

} }

cyoa{ road{ 

// Common used elements for road block encounters

cyoa-choice: car-detour
	($ Thegroup .. " 'decide tomar un desvio! Esto suele ser una mala idea." $) cyoa-text!
	detour-roll
;

// Used to tune up a car in a garage, before it's broken
: car-tuneup // partially combined with weldup now
	car-engine car-max-engine >= if
		"El coche corre como nuevo! " 
	else
		car-engine 14 > if
			"El coche corre bien. "
		then
		car-engine 15 < car-engine 7 > and if
			"El coche ha visto dias mejores. "
		then
		car-engine 8 < if
			"El coche puede romperse en cualquier momento. "
		then
	then
	+crtext
;
cyoa-choice: car-weldup
	($ cname .. "  intenta reparar el coche con la esperanza de que aguante un poco maÌs antes de romperse. " $) cyoa-text!
	mechanic-weldup +text
	car-chassis car-max-chassis = if
		"El coche parece no tener danos!" 
	else
		car-chassis 3 < if
			"El coche todavia se ve todo destrozado."
		else
			"El coche parece todo golpeado, pero aun solido."
		then
	then
	car-tuneup
	+text mechanicREV mechanic-diff -2 > if mechanic++ then +cr mechanic-day?
;

// Standard choices in every garage
: garage-choices
	mechanicest choosechar
	choice( "Reparacion y afinacion de autos" )choice: car-weldup
	// choice( "Afinacion completa" )choice: car-tuneup 
	choice( "Alejarse" )choice 
;

cyoa: car-nogas
	"SIN GASOLINA" cyoa-title!
	($ "El coche se ha quedado sin gasolina completamente, y se detiene." .. "EL CARRO FUE ABANDONADO!" rbold $) cyoa-text! badfx car-lost
;

cyoa-choice: car-abandoned
	($ "Sin perder tiempo, " .. thegroup .. " decide dejar el coche y caminar hasta que encuentren un nuevo vehiculo." $) cyoa-text!
	car-abandon
;
cyoa-choice: car-breakdown-punch
	($ cname .. " abre el capo del coche y comienza a GOLPEAR." $) cyoa-text! car-strengthcheck strengthREV
;
cyoa-choice: car-breakdown-fix
	($ cname .. " abre el capo del coche." $) cyoa-text! car-tunecheck mechanicREV
;
cyoa: car-breakdown
	"AVERIA DEL COCHE" cyoa-title!
	($ "El coche se ha averiado por uso y abuso. No volvera a arrancar." $) cyoa-text!
		choice( "Abandona el coche" )choice: car-abandoned
		mechanicest charchoice( " 'intenta arreglarlo" )choice: car-breakdown-fix
		someone charchoice( " 'golpea el coche" )choice: car-breakdown-punch
		
;

} }
